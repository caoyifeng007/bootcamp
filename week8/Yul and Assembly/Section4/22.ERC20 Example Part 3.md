```solidity
            // "totalSupply()"
            case 0x18160ddd {
                returnUint(totalSupply())
            }
            
            function totalSupplyPos() -> p { p := 1 }
            
            function totalSupply() -> supply {
                supply := sload(totalSupplyPos())
            }
```

Now let's look at totalSupply, this is actually going to behave quite predictably, it's returning a unit which is the total coins in circulation, and that's going to come from this function which will simply sload the totalSupplyPos, so it's just going to return whatever is in the slot returned by this function, which is slot1, that's quite straightforward. 

```solidity
            // "mint(address,uint256)"
            case 0x40c10f19 {
                mint(decodeAsAddress(0), decodeAsUint(1))
                returnTrue()
            }
            
            function mint(account, amount) {
                require(calledByOwner())

                mintTokens(amount)
                addToBalance(account, amount)
                emitTransfer(0, account, amount)
            }
            
            function calledByOwner() -> cbo {
                cbo := eq(owner(), caller())
            }
```

Let's look at how totalSupply changes, that happens when the mint function is called, so mint is of course going to call this internal implementation of it, and the first thing it will do is check that only the owner is calling it, that is pretty obvious what happens here, it just takes the caller and checks if it's equal to the owner, we already know that this is just going to sload whatever is in slot0. 

```solidity
            function mintTokens(amount) {
                sstore(totalSupplyPos(), safeAdd(totalSupply(), amount))
            }
```

So after that check passes, it's going to call mintTokens, and mintTokens is going to get the totalSupplyPos which we saw to be 1 earlier, and store into that, the addition of the amount that is minted with what is already there. 

```solidity
            function safeAdd(a, b) -> r {
                r := add(a, b)
                if or(lt(r, a), lt(r, b)) { revert(0, 0) }
            }
```

So what is safeAdd doing? Well, when you add two numbers together, you can detect an overflow if the sum of them is less than either of the components of the sum, because it doesn't make sense to add two numbers together and then get a smaller number, that can only happen when an overflow happens, we check if the result is less than a, and we check if the result is less than b, if either of those situations happened, then we will revert. 

```solidity
            function mint(account, amount) {
                require(calledByOwner())

                mintTokens(amount)
                addToBalance(account, amount)
                emitTransfer(0, account, amount)
            }
            
            function addToBalance(account, amount) {
                let offset := accountToStorageOffset(account)
                sstore(offset, safeAdd(sload(offset), amount))
            }
            
            function accountToStorageOffset(account) -> offset {
                offset := add(0x1000, account)
            }
```

So that is what mintTokens is doing. Let's proceed. It's going to addToBalance now, and this is where it's going to simulate the mapping of address to the amount of coins that that person owns, so accountToStorageOffset again is just the address plus hex 1000, and this plus 1000 ensures that it will not crash into either of these two variables (owner和totalSupply), so addToBalance is simply going to get where that storage slot is, load the value from over there, add the amount that we're incrementing that person's balance by, add them together, and store them into the offset or rather where that person's account is. 

```solidity
            function emitTransfer(from, to, amount) {
                let signatureHash := 0xddf252ad1be2c89b69c2b068fc378daa952ba7f163c4a11628f55a4df523b3ef
                emitEvent(signatureHash, from, to, amount)
            }
            
            function emitEvent(signatureHash, indexed1, indexed2, nonIndexed) {
                mstore(0, nonIndexed)
                log3(0, 0x20, signatureHash, indexed1, indexed2)
            }
```

And then finally it's going to emit a Transfer, so this is the first time that we're going to look at events in this contract, and the signatureHash is just the keccak256 of the event signature, it's somewhat coincidental in ERC20 that you only under the hood need to use a log3 when you are either emitting an approval or a emit transfer. Now when you are ~~emitting~~, defining and emitting events in solidity, of course you define their type, but under the hood everything is just 32 bytes, so that doesn't matter, (指yul中event的参数不需要类型), after this emitTransfer is emitted, that concludes the emit process.

```solidity
    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply += amount;
        unchecked {
            // Overflow not possible: balance + amount is at most totalSupply + amount, which is checked above.
            _balances[account] += amount;
        }
        emit Transfer(address(0), account, amount);

        _afterTokenTransfer(address(0), account, amount);
    }
```

If you compare it to the OpenZeppelin implementation, you will see something pretty familiar. First, it will check if the address is not 0, it didn't explicitly do that in the code that we were looking at, however, we saw earlier with how we parsed addresses, that zero addresses are rejected. The _beforeTokenTransfer and _afterTokenTransfer hooks are just convenience functions inside of OpenZeppelin, they're not part of the ERC20 specification, but we update the _totalSupply just like we saw earlier, and then we increase the balance of the account that we are minting to, and then emit the Transfer which has the three arguments in it.