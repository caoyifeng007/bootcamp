```solidity
// "totalSupply()"
case 0x18160ddd {
	returnUint(totalSupply())
}

function totalSupplyPos() -> p { p := 1 }

function totalSupply() -> supply {
	supply := sload(totalSupplyPos())
}
```

Now let's look at **totalSupply**, this is actually going to behave quite predictably, it's returning a unit which is the total coins in circulation, and that's going to come from this function which will simply **sload** the **totalSupplyPos**, so it's just going to return whatever is in the slot returned by this function (**totalSupplyPos**), which is slot 1, that's quite straightforward. 

```solidity
// "mint(address,uint256)"
case 0x40c10f19 {
    mint(decodeAsAddress(0), decodeAsUint(1))
    returnTrue()
}

function mint(account, amount) {
    require(calledByOwner())    // line1

    mintTokens(amount)
    addToBalance(account, amount)
    emitTransfer(0, account, amount)
}

function calledByOwner() -> cbo {
    cbo := eq(owner(), caller())
}
```

Let's look at how **totalSupply** changes, that happens when the **mint** function is called, so **mint** is of course going to call this internal implementation of it, and the first thing it will do is check that only the owner is calling it, that is pretty obvious what happens here, it just takes the **caller()** and checks if it's equal to the **owner()**, we already know that this is just going to sload whatever is in slot 0. 

```solidity
function mintTokens(amount) {
	sstore(totalSupplyPos(), safeAdd(totalSupply(), amount))
}
```

So after that (line1) check passes, it's going to call **mintTokens**, and **mintTokens** is going to get the **totalSupplyPos** which we saw to be 1 earlier, and store into that, the addition of the amount that is minted with what is already there (指现在的**totalSupply**).

```solidity
function safeAdd(a, b) -> r {
    r := add(a, b)
    if or(lt(r, a), lt(r, b)) { revert(0, 0) }
}
```

So what is **safeAdd** doing? Well, when you add two numbers together, you can detect an overflow if the sum of them is less than either of the components of the sum, because it doesn't make sense to add two numbers together and then get a smaller number, that can only happen when an overflow happens, we check if the result is less than a, and we check if the result is less than b, if either of those situations happened, then we will revert. 

```solidity
function mint(account, amount) {
    require(calledByOwner())

    mintTokens(amount)
    addToBalance(account, amount)
    emitTransfer(0, account, amount)
}

function addToBalance(account, amount) {
    let offset := accountToStorageOffset(account)
    sstore(offset, safeAdd(sload(offset), amount))
}

// -------- storage layout ---------- */
function ownerPos() -> p { p := 0 }
function totalSupplyPos() -> p { p := 1 }  
function accountToStorageOffset(account) -> offset {
	offset := add(0x1000, account)
}
```

So that is what **mintTokens** is doing, let's proceed, it's going to **addToBalance** now, and this is where it's going to simulate the mapping of address to the amount of coins that that person owns, so **accountToStorageOffset** again is just the address plus hex 1000, and this plus 1000 ensures that it will not crash into either of these two variables.(Solidity中mapping的底层实现就是对mapping所在是slot和相应的key放到一起做keccak256，得到的哈希值就是这个key在合约中的slot，然后在这个slot上存储对应的value值，详情可参考第6节，**addToBalance**就是相当于模拟了一个**mapping(address => uint256)**，因为address本身就是哈希函数生成的，所以就可以略过keccak256这一步直接作为slot使用，这里加1000这就保证不会和**owner**和**totalSupply**这两个state variable冲突，一个是slot 0，一个是slot 1，就算某个地址刚好是0，加上1000也就不会和**owner**冲突)

```solidity
function addToBalance(account, amount) {
    let offset := accountToStorageOffset(account)
    sstore(offset, safeAdd(sload(offset), amount))
}
```

So **addToBalance** is simply going to get where that storage slot is, load the value from over there, add the **amount** that we're incrementing that person's balance by, add them together, and store them into the offset or rather where that person's account is. (先获取参数中指定的account在合约中的slot值，然后获取slot上现在的值，加上amount后再存回去)

```solidity
//* ------------------- Solidity ------------------- *//
interface IERC20 {
	event Transfer(address indexed from, address indexed to, uint256 value);  // line1
}

//* ---------------------- yul ---------------------- *//
function emitTransfer(from, to, amount) {
	// keccak256("Transfer(address,address,uint256)")
	// keccak256(abi.encodePacked("Transfer(address,address,uint256)"))
	// 上边两行的结果都是 signatureHash
    let signatureHash := 0xddf252ad1be2c89b69c2b068fc378daa952ba7f163c4a11628f55a4df523b3ef
    emitEvent(signatureHash, from, to, amount)
}
function emitApproval(from, spender, amount) {
    let signatureHash := 0x8c5be1e5ebec7d5bd14f71427d1e84f3dd0314c0f7b2291e5b200ac8c7c3b925
    emitEvent(signatureHash, from, spender, amount)
}
function emitEvent(signatureHash, indexed1, indexed2, nonIndexed) {
    mstore(0, nonIndexed)
    log3(0, 0x20, signatureHash, indexed1, indexed2)
}
```

And then finally it's going to emit a **Transfer**, so this is the first time that we're going to look at events in this contract, and the **signatureHash** is just the **keccak256** of the event signature (指line1 event的签名).

It's somewhat coincidental in ERC20 that you only, under the hood, need to use a **log3** when you are either **emitApproval** or a **emitTransfer**. (意思是说：有点巧合的是，在这个yul实现的ERC20合约中调用**emitApproval**和**emitApproval**，底层上只能用到**log3**，用不到其他的log函数)

Now when you are emitting... defining and emitting events in Solidity, of course you define their type, but under the hood everything is just 32 bytes, so that doesn't matter, (应该是说不管Solidity中类型是什么，底层都是32个字节，yul中也只有32 bytes一个类型), after this **emitTransfer** is emitted, that concludes the emit process.



```solidity
function _mint(address account, uint256 amount) internal virtual {
    require(account != address(0), "ERC20: mint to the zero address");

    _beforeTokenTransfer(address(0), account, amount);

    _totalSupply += amount;
    unchecked {
        // Overflow not possible: balance + amount is at most totalSupply + amount, which is checked above.
        _balances[account] += amount;
    }
    emit Transfer(address(0), account, amount);    // line1

    _afterTokenTransfer(address(0), account, amount);
}
```

If you compare it to the OpenZeppelin implementation, you will see something pretty familiar. First, it will check if the address is not 0, it didn't explicitly do that in the code that we were looking at, however, we saw earlier with how we parsed addresses, that **zero addresses** are rejected, the **_beforeTokenTransfer** and **_afterTokenTransfer** hooks are just convenience functions inside of OpenZeppelin, they're not part of the ERC20 specification, but we update the **_totalSupply** just like we saw earlier, and then we increase the balance of the **account** that we are minting to, and then emit the **Transfer** (line1) which has the three arguments in it.