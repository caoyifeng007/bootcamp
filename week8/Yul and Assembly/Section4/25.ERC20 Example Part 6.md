```solidity
    function _approve(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }
```

Before we understand the transferFrom function, we need to understand approval, first, let's look at the OpenZeppelin implementation, over here you can see that it is simply a mapping from the owner to the spender, and the allowance that the spender has (amount), this functionality is usually used by smart contracts to be able to withdraw tokens from your account. 

For example, when you conduct a trade on Uniswap or when you bid for an NFT on Opensea and the seller accepts your bid, that enables the Opensea smart contract to withdraw the tokens from your wallet. 

As we reviewed earlier, a double mapping in solidity is the hash of the concatenation of the storage slot and the keys in the map, but yul doesn't have to follow this mechanism exactly. You can design your hash maps in whatever way make sense in your application. 

```solidity
            function accountToStorageOffset(account) -> offset {
                offset := add(0x1000, account)
            }
            
            function allowanceStorageOffset(account, spender) -> offset {
                offset := accountToStorageOffset(account)
                mstore(0, offset)
                mstore(0x20, spender)
                offset := keccak256(0, 0x40)
            }
```

In this example, what they are doing is they are taking the storage slot of the account balance, storing that in the first slot (line1), in the first word in memory, then storing the spender in the second slot in memory (line2), and then hashing those two together, 0x40 is the same as 64 bytes which would be the sum of both of these memory words. Note that the storage slot is not included inside of this hash, that is not necessary because there is no other place in this contract where two addresses are being hashed together to get a storage slot. If that was the case, we might have a storage collision, that is why solidity concatenate the storage slot when it is doing double mappings. But because we know this is the only situation in this smart contract where addresses are combined in this way we don't have to worry about a hash collision. 

I do think it's a bit of an interesting design choice that they decided to concatenate the address added with hex 1000 to the spender, if I were designing this, I would have just put the address of the owner and the address of the spender over here to avoid this extra step, but it works. 

```solidity
            // "approve(address,uint256)"
            case 0x095ea7b3 {
                approve(decodeAsAddress(0), decodeAsUint(1))
                returnTrue()
            }
            
            function approve(spender, amount) {
                revertIfZeroAddress(spender)
                setAllowance(caller(), spender, amount)
                emitApproval(caller(), spender, amount)
            }
```

To update this (allowanceStorageOffset) is straightforward enough, it's done via the approve function, which follows the same sequence we were familiar with, at this point, zero addresses are not valid, and then it's going to set the allowance for the spender associated with the person who's calling the function for the given allowance. 

```solidity
            function setAllowance(account, spender, amount) {
                sstore(allowanceStorageOffset(account, spender), amount)
            }
            
            // "transferFrom(address,address,uint256)"
            case 0x23b872dd {
                transferFrom(decodeAsAddress(0), decodeAsAddress(1), decodeAsUint(2))
                returnTrue()
            }
```

All that does is get the storage offset, which is the hash we described earlier and store the allowance there, therefore, when transferFrom is called, it's going to call the actual implementation of it and do the executeTransfer as we described earlier. 

```solidity
            function transferFrom(from, to, amount) {
                decreaseAllowanceBy(from, caller(), amount)
                executeTransfer(from, to, amount)
            }
            
            function decreaseAllowanceBy(account, spender, amount) {
                let offset := allowanceStorageOffset(account, spender)
                let currentAllowance := sload(offset)
                require(lte(amount, currentAllowance))
                sstore(offset, sub(currentAllowance, amount))
            }
```

But first, it's going to decrease the allowance of the spender. It should be pretty obvious what's going on by this point, we're going to get that allowance storage slot, which was computed in the way we described earlier, load the allowance, make sure that they are spending less than what their allowances or less than or equal to, and then store the reduced balance inside of that storage location, again, we do not have to worry about underflow. 

```solidity
            function lte(a, b) -> r {
                r := iszero(gt(a, b))
            }
            function gte(a, b) -> r {
                r := iszero(lt(a, b))
            }
```

Now remember, less than or equal to is not actually a opcode inside of a yul, they created a custom implementation here, which is simply iszero greater then and this is what solidity does under the hood when you use a less than or equal to operator because there is no opcode that maps directly to this. 

So this wraps up the ERC20 yul code review, as a side comment, I'd like to note that this contract is actually quite easy to understand because it uses a lot of small functions that are very well named. The yul optimizer and the recent upgrades in the solidity compiler are pretty smart about enlightening functions, so I wouldn't worry about the extra costs of making all of these extra function calls to do simple things. 

One more thing to note is that there is no free memory pointer anywhere inside of this code, this particular application doesn't use arrays or structs anywhere, so it isn't necessary, not having the extra step to allocate the free memory pointer will actually create a smart contract that is more efficient. 

Solidity will always allocate a free memory pointer and load it into 0x40 in memory regardless of whether or not it's actually used, this leads to unnecessary work and a larger smart contract, maybe a future version of the solidity compiler will optimize this, but that isn't the case as of the time I'm recording this video.