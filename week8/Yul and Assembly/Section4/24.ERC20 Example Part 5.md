```solidity
// "transfer(address,uint256)"
case 0xa9059cbb {
    transfer(decodeAsAddress(0), decodeAsUint(1))
    returnTrue()
}

// "transferFrom(address,address,uint256)"
case 0x23b872dd {
    transferFrom(decodeAsAddress(0), decodeAsAddress(1), decodeAsUint(2))
    returnTrue()
}
```

The last two functions to review in this code are **transfer** and **transferFrom**, now by way of review, ERC20 has two mechanisms for transferring tokens, **transfer** is called when the owner is trying to move his own tokens, and **transferFrom** can be used either by the owner or by an approved spender, so if you give a different address approval to spend your tokens, they would use the **transferFrom** mechanism to move your tokens on your behalf.

```solidity
function executeTransfer(from, to, amount) {
    revertIfZeroAddress(to)
    deductFromBalance(from, amount)
    addToBalance(to, amount)
    emitTransfer(from, to, amount)
}
```

So under the hood, both of these functions are going to call **executeTransfer**.

```solidity
function transfer(to, amount) {
	executeTransfer(caller(), to, amount)      // line1
}

function transferFrom(from, to, amount) {
    decreaseAllowanceBy(from, caller(), amount)
    executeTransfer(from, to, amount)
}

function executeTransfer(from, to, amount) {
    revertIfZeroAddress(to)
    deductFromBalance(from, amount)
    addToBalance(to, amount)
    emitTransfer(from, to, amount)
}
```

You can see this function signature relating to **transfer** (selector为0xa9059cbb的函数) calling the **transfer** function over here and **transferFrom** (selector为0x23b872dd的函数) is going to call **transferFrom** which will also eventually call **executeTransfer**.

So let's look at what's happening, **transfer** is calling **executeTransfer**, but it's going to inject the **caller()** into it (line1), because **executeTransfer** needs the source (指**from**), the destination (指**to**) and the amount, again, as we said, **transfer** assumes that the owner is trying to move his own tokens.

```solidity
function executeTransfer(from, to, amount) {
    revertIfZeroAddress(to)
    deductFromBalance(from, amount)
    addToBalance(to, amount)
    emitTransfer(from, to, amount)
}
```

In the previous video, we already looked at **emitTransfer** and **addToBalance**, so we won't review that again, the new functions here are **revertIfZeroAddress** and **deductFromBalance**, now I'm not sure why they have this function over here because we already noted that the address parser doesn't accept **zero addresses**. (他是指**decodeAsAddress**这个函数不接受zero address，但是那个地方他肯定是讲错了)

```solidity
function deductFromBalance(account, amount) {
    let offset := accountToStorageOffset(account)
    let bal := sload(offset)
    require(lte(amount, bal))   // line1
    sstore(offset, sub(bal, amount))
}
```

Let's look at **deductFromBalance**, it's going to get the account storage as we had looked at earlier and load the balance from it, normally you could do this in one step, but you'll see in a second why they stored the variables separately, to ensure that people aren't transferring more tokens that they are in possession of, we need to make sure that the **amount** they are transferring is in fact less than their balance.

And then we are going to store the deducted amount back into the offset that corresponds to their storage balance, over here this is a regular subtraction, in the previous video, we noted that this code used a function called **safeAdd**, but we don't have to worry about underflow here, because right earlier we already checked that amount is in fact less than balance, so that we know it can't underflow. (因为在line1这行已经做过校验了，所以可以确定不会发生underflow)





```solidity
function _transfer(
    address from,
    address to,
    uint256 amount
) internal virtual {
    require(from != address(0), "ERC20: transfer from the zero address");
    require(to != address(0), "ERC20: transfer to the zero address");

    _beforeTokenTransfer(from, to, amount);

    uint256 fromBalance = _balances[from];
    require(fromBalance >= amount, "ERC20: transfer amount exceeds balance");
    unchecked {
        _balances[from] = fromBalance - amount;
        // Overflow not possible: the sum of all balances is capped by totalSupply, and the sum is preserved by
        // decrementing then incrementing.
        _balances[to] += amount;
    }

    emit Transfer(from, to, amount);

    _afterTokenTransfer(from, to, amount);
}
```

In the OpenZeppelin implementation, it is exactly the same, the source (**from**) and destination (**to**) addresses cannot be zero, you can't transfer more than the amount that is inside of your balance, and the source has its balance deducted by the amount and the destination has its balance incremented by the amount, and then we emit a Transfer. (这里，OpenZeppelin的实现是from和to都不能是0地址，但是上边yul的实现只有to不能是0地址)