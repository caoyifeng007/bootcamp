Memory is an important part of yul because it allows us to deal with data types that are larger than 32 bytes. For example, if we wanted to return a struct or an array from a function, this would not be possible with a single variable if that struct or array is larger than 32 bytes. 

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.17;

contract UsingMemory {
    function return2and4() external pure returns (uint256, uint256) {
        assembly {
            mstore(0x00, 2)
            mstore(0x20, 4)
            return(0x00, 0x40)
        }
    }
}
```

So in this example, what we are doing is loading the value 2 into the first word in memory (a word is 32 bytes in yul) and 4 into the next word, and then we simply return the boundaries of the area in memory that we are trying to return.

[return(p, s)](https://docs.soliditylang.org/en/latest/yul.html#evm-dialect) - end execution, return data mem[p…(p+s)]   yul文档中对return的描述

Note that **return** in yul, returns an area in memory specified by the arguments, it's not returning a variable like it does inside of Solidity, now under the hood, this is actually what Solidity is doing, but you have to be a lot more explicit with yul. (这段意思是Solidity中return一个变量，底层其实也是和yul一样，return一个内存区域)

![](https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section2/yulreturn.png)

This works as expected, note that the syntax highlighting here (指return是绿色的) is a little bit misleading, return is a function inside of yul, it's not necessarily a special keyword. (意思是，return在yul中是function，不是关键字，绿色会让人误以为是关键字)

![](https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section2/return2and4.png)

When we call **return2and4**, we get 2 and 4 back as expected. 





```solidity
function requireV1() external view {
	require(msg.sender == 0xAb8483F64d9C6d1EcF9b849Ae677dD3315835cb2);
}

function requireV2() external view {
    assembly {
        if iszero(eq(caller(), 0xAb8483F64d9C6d1EcF9b849Ae677dD3315835cb2)) {
            revert(0, 0)   // line1
        }
    }
}
```

When you want to revert an execution, for example, due to access control or some other invalid state you also need to specify an area in memory to return, just like **return**, because what Solidity kind of obscures from yul is that in a revert case, it's still possible to return data so that the calling function can respond to it and do something about it.

Most of the time, however, when you are reverting, it's because you want execution to stop and you're not trying to return values. So this (line1) is what you are going to see most of the time, but both of these functions are going to revert because I'm calling them with the wrong address. (requireV1和requireV2两种revert方式)

So let's pull up the console and when we call the... called the hash function, sorry, so when we call **requireV1**, we see a revert and **requireV2** we see a revert, both of these are implementing the same logic of checking the **msg.sender** and then checking if it's equal to this particular address over here.

(这段意思应该是在yul中达到Solidity的require一样的效果用revert，revert和之前的return一样都是返回一段内存区域)



```solidity
function hashV1() external pure returns (bytes32) {
    bytes memory toBeHashed = abi.encode(1, 2, 3);

    // return keccak256(abi.encode(1, 2, 3));  // line1
    return keccak256(toBeHashed);
}
```

```solidity
// compute the Keccak-256 hash of the input
// 文档中 keccak256 的定义
keccak256(bytes memory) returns (bytes32)
```

In regular Solidity, **keccak256** takes a variable that is of type **bytes memory**, and in this case we are hashing the sequence 1, 2, 3 which is uint256 of 1, 2 and 3 laid out end to end in memory, note how this is explicitly in memory, but if I had just put a **abi.encode** inside of here (像line1那样把abi.encode直接放到keccak256中), then the fact that it's in memory, is a little bit obscured. (意思是line1的写法没毛病，keccak256需要的参数实际上是在memory中，但是不能一眼就看出来)

```solidity
// ABI-encodes the given arguments
abi.encode(...) returns (bytes memory)
```

(根据文档定义，abi.encode返回的数据是在memory上的)



```solidity
function hashV2() external pure returns (bytes32) {
    assembly {
        let freeMemoryPointer := mload(0x40)

        // store 1, 2, 3 in memory
        mstore(freeMemoryPointer, 1)
        mstore(add(freeMemoryPointer, 0x20), 2)
        mstore(add(freeMemoryPointer, 0x40), 3)

        // update memory pointer
        mstore(0x40, add(freeMemoryPointer, 0x60)) // increase memory pointer by 96 bytes

        mstore(0x00, keccak256(freeMemoryPointer, 0x60))  // line1
        return(0x00, 0x20)
    }
}
```

In yul, this is always explicit, it takes arguments, the starting point in memory (line1，keccak256的参数：**freeMemoryPointer**) and how many bytes you want to hash (**0x60**), as part of its arguments, so in this example, we've loaded 1, 2 and 3 explicitly into memory, and this whole sequence is hex 60 or 96 bytes, that's why we have supplied this over here, and this is where we started writing it.

[keccak256(p, n)](https://docs.soliditylang.org/en/latest/yul.html#evm-dialect) -- keccak(mem[p…(p+n)))

So this **keccak256** (这个keccak256是yul中的，不是Solidity中的那个), we'll look inside of that area and it itself returns a 32 byte value which we are going to store in the zeroth slot and then we will return it. (这个地方的个人理解：Solidity的keccak256的参数需要abi.encode来返回，而abi.encode对传入它的每个参数补足32字节之后放到memory中，每个参数在memory中都是头尾相接的，然后再将这段memory传入keccak256，keccak256对这段memory做哈希运算得出最终的结果，这一切都是需要用到memory的。而yul中的keccak256，从定义就可以看出来，参数就是一段memory，而这段memory需要自己手动去放入参数。)

<img src="https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section2/hashv1andhashv2.png" style="zoom:80%;" />

So this will work as expected, both the regular hash and our more low level implementation of it return the same values back.

```solidity
// return2and4 中是0x00-0x40，2个32 bytes，而return2and4的返回值是两个uint256
return(0x00, 0x40)  =>  returns (uint256, uint256)

// hashV2 中是0x00-0x20，1个32 bytes，而hashV2的返回值是bytes32
return(0x00, 0x20)  =>  returns (bytes32)
```

It's worth emphasizing that these arguments are a little bit different to what we saw earlier with **return**, **return** (指**return2and4**函数中的return) you pass in, the beginning and the end, these are the explicit memory addresses. Over in **hashV2** you supply the beginning and how many bytes total. (其实本质是一样的，都是返回一个memory区间，只不过第一个是返回一个bytes32，即1个256，另一个是返回2个uint256)

One thing to note about the return value is that the compiler is not going to enforce that your return signature matches what is inside of return. (返回的返回类型不比和return的内存区域匹配)

```solidity
function hashV2() external pure returns (bytes32) {
    assembly {
        let freeMemoryPointer := mload(0x40)

        // store 1, 2, 3 in memory
        mstore(freeMemoryPointer, 1)
        mstore(add(freeMemoryPointer, 0x20), 2)
        mstore(add(freeMemoryPointer, 0x40), 3)

        // update memory pointer
        mstore(0x40, add(freeMemoryPointer, 0x60)) // increase memory pointer by 96 bytes

        mstore(0x00, keccak256(freeMemoryPointer, 0x60))  // line1
        return(0x00, 0x10)
    }
}
```

So for example, if I were to do something funny like this, I put in 0x10, so I'm only returning 16 bytes.

<img src="https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section2/hashv2.png" style="zoom:80%;" />

And then I recompile and run this thing and run **hashV2**, the client doesn't like it because it's getting less bytes back than it expected, however, the transaction itself was successful, but the client doesn't know what it's looking at because it's expecting it to be 32 bytes. (函数返回类型要求bytes32，但是return的返回的内存区域只有16个字节)

```solidity
return(0x00, 0x60)  =>  returns (bytes32)
```

Similarly, I could put in a much larger value and say return 96 bytes even though I only wrote 32 to memory, so in this scenario it's going to work as expected, but what's really happening is it's returning more bytes, but the client is just ignoring those bytes because it's explicitly only looking for the first 32. (这段的意思是，return返回的内存区域是96个字节，但是函数的返回类型是byte32，所以remix会自动截取这96个字节的前32个字节，所以一切正常)



In this example, we were dealing with more than 64 bytes. Solidity reserves, the first 64 bytes for scratch space that you can do whatever you like with. 

```solidity
assembly {
    mstore(0x00, 2)
    mstore(0x20, 4)
    return(0x00, 0x40)
}
```

So in the first example, we just loaded the values into that scratch space and return them. 

```solidity
mstore(add(freeMemoryPointer, 0x40), 3)
```

In this example, that would not be a good idea because this would crash into the free memory pointer. 

```solidity
function hashV2() external pure returns (bytes32) {
    assembly {
        let freeMemoryPointer := mload(0x40)

        // store 1, 2, 3 in memory
        mstore(freeMemoryPointer, 1)
        mstore(add(freeMemoryPointer, 0x20), 2)
        mstore(add(freeMemoryPointer, 0x40), 3)

        // update memory pointer
        mstore(0x40, add(freeMemoryPointer, 0x60)) // increase memory pointer by 96 bytes

        mstore(0x00, keccak256(freeMemoryPointer, 0x60))
        return(0x00, 0x60)
    }
}
```

So what instead we did is load up the free memory pointer, which tells us where we can start writing things without collision, and then we start writing, we update the free memory pointer and then we run our hash function on that. Now, technically, this is not totally necessary in this scenario because when we call return, we're handing control back to the calling contract and our memory is going to be erased. However, I want to illustrate what good practice looks like in this code. (这段意思就是说要自己手动更新free memory pointer，这样之后的再使用memory才不会重叠出bug)