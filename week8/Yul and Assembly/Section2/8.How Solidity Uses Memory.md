So here is how solidity expects memory to be used. 

![](howsolidityusememory.png)

Solidity reserves slots 0 through 63 or 0 through 0x40 for "scratch space", so you can just write values here and expect them to be ephemeral. It also might mean that some previous operation might have left over some value here because this space is not guaranteed to be cleared, but if you were writing in 32 byte increments, it's going to overwrite whatever slot you're writing into.

The slot 0x40 through 0x60, not inclusive, so it's 0x40 through 0x5f is the "free memory pointer", that is, if you want to write something new to memory, this is where you would start writing it, and it's guaranteed to not crash into anything. 

Because solidity does not garbage collect or free memory, then the free memory pointer will never decrement as the transaction progresses, solidity proceeds to keep 0x60 empty and the action begins in 0x80. So as you are writing structs and arrays and other things to memory, it will begin at 0x80 and go on from there. 

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.17;

contract Memory {
    struct Point {
        uint256 x;
        uint256 y;
    }

    event MemoryPointer(bytes32);
    
		function memPointer() external {
        bytes32 x40;
        assembly {
            x40 := mload(0x40)
        }
        emit MemoryPointer(x40);
        Point memory p = Point({x: 1, y: 2});

        assembly {
            x40 := mload(0x40)
        }
        emit MemoryPointer(x40);
    }
}
```

We said that solidity stores the free memory pointer in 0x40, so what we're going to do is start a fresh transaction, look at what's inside of the free memory pointer, and then we're going to allocate a struct on memory, the struct is going to consist of two 32 byte integers or 256 bits, so we expect the free memory pointer to be advanced by that amount when we read it a second time. 

Just to make things simple, I'm going to emit the memory pointer as an event which is bytes 32 so that it becomes easier to read. 

![](log.png)

Let's run this thing, we run memPointer and look at the transaction, the first log that happens is memory pointer and it says 80, that's where we said the action begins, that makes sense. 

![](log2.png)

The log happens a second time and says c0.

<img src="minus.png" alt="1" style="zoom:70%;" />

So how do we interpret that? Well, if we just subtract these values, 0xc0 - 0x80, we get 64, and of course, 64 is two sets of 32 which is the x and the y that we had. 

```solidity
contract Memory {
    struct Point {
        uint256 x;
        uint256 y;
    }

    event MemoryPointerMsize(bytes32, bytes32);
    
    function memPointerV2() external {
        bytes32 x40;
        bytes32 _msize;
        assembly {
            x40 := mload(0x40)
            _msize := msize()
        }
        emit MemoryPointerMsize(x40, _msize);

        Point memory p = Point({x: 1, y: 2});
        assembly {
            x40 := mload(0x40)
            _msize := msize()
        }
        emit MemoryPointerMsize(x40, _msize);

        assembly {
            pop(mload(0xff))
            x40 := mload(0x40)
            _msize := msize()
        }
        emit MemoryPointerMsize(x40, _msize);
    }
}
```

Now let's do this same exercise again, but we'll also illustrate what msize is doing. So I've created an event that does something similar to last time, except that it will allow me to emit two numbers in 32 byte format. 

So we will start with nothing, then we will allocate that point and we will look at what's inside of the free memory pointer as well as what is the largest accessed byte, and then finally we're going to access a byte far into the future, look at the free memory pointer, and then what was the farthest access byte, of course, we expect in this case that the free memory pointer will not be updated because solidity doesn't know about this operation. ( **pop(mload(0xff))** )

When solidity code is compiled, it automatically does bookkeeping on this, but since we're hooking into assembly mode, we're bypassing that bookkeeping, in this situation, it's not consequential because we're only reading things, we're not writing them. We'll show later how this might actually matter, but for now, let's not worry about it. 

![](log3.png)

Let's run this transaction and see what gets output. At first we see solidity writing the free memory pointer to 0x40 through 0x60 which is why msize is 0x60, but the free memory pointer is 0x80. Remember that there's a gap that solidity leaves before it starts writing things.

![](log4.png)

After the point consisting of two numbers as written, then we have the same result as last time, the free memory pointer and msize point to the same slot because now solidity is not going to be leaving any gaps. 

![](log5.png)

And finally, during this operation ( **pop(mload(0xff))** ), when we read a byte into the future but don't write anything and then look where the free memory pointer is, of course, the free memory pointer has not changed from last time. However, msize has changed as we expect because we had read from a further away byte. 

```solidity
    function fixedArray() external {
        bytes32 x40;
        assembly {
            x40 := mload(0x40)
        }
        emit MemoryPointer(x40);
        uint256[2] memory arr = [uint256(5), uint256(6)];
        assembly {
            x40 := mload(0x40)
        }
        emit MemoryPointer(x40);
    }
```

Arrays of a fixed length will under the hood behave the same way as the struct we looked at earlier, so the struct we had earlier was two uint256 values and we have the same here. 

![](log6.png)

So let's run this, and when I run fixedArray and then look at the logs, I'm going to see that, first we have the usable 80 at the initialization stage.

![](log7.png)

And then the free memory pointer is advanced 64 bytes into the future c0, you can think of this as 80 plus 20 in hex is going to become a0, and then add another 20 to that, that's going to become c0. 

```solidity
    function abiEncode() external {
        bytes32 x40;
        assembly {
            x40 := mload(0x40)
        }
        emit MemoryPointer(x40);
        abi.encode(uint256(5), uint256(19));
        assembly {
            x40 := mload(0x40)
        }
        emit MemoryPointer(x40);
    }
```

Now let's look at what happens when we use abi.encode, the output of abi.encode needs to go either into storage or memory, so if I tried to assign this to a value like bytes x is equal to abi.encode, then solidity is going to complain because it must be "storage" or "memory" or "calldata", well, of course, it can't be calldata because it's not an argument, I don't know why it's saying that. Anyway, let's note that this is really going into memory and now the compiler is happy, but we don't care about that, so I'm just gonna get rid of this. 

![](log8.png)

![](log9.png)

This might seem similar to the situation we had above, where we just have a struct or an array of two values, but when we run this thing and look at the logs, we're going to see, okay, the free memory pointer began at 80 as usual, but then it advanced to e0, well, that's interesting, because earlier it advanced to c0.

So why is it 20 hex bytes into the future? Well, the reason for that is because encode needs to know how many items are inside of here or more generally how many bytes are inside of here. So the first thing it's going to put into memory is the length of how much data follows. 

<img src="debugmemory.png" style="zoom:80%;" />

Let's look at our friend the debugger and see what's in memory, so I'm going to jump almost to the very end before the memory is cleared. Look in the memory and here we can see the memory pointer has gone to e0, we see an interesting value here of 40 which is the same as 64, and we see a 5 and a 13, now the 13 corresponds to the 19, let me try to pull it up in this little region here, this 19 over here, if you translate decimal to hex is going to become 13 and the 5 is of course 5, so this 40 over here, this additional item is saying that there follows 0x40 more bytes.

```solidity
    function abiEncode2() external {
        bytes32 x40;
        assembly {
            x40 := mload(0x40)
        }
        emit MemoryPointer(x40);
        abi.encode(uint256(5), uint128(19)); // 注意第二个参数类型变为uint128了
        assembly {
            x40 := mload(0x40)
        }
        emit MemoryPointer(x40);
    }
```

We will get the same results if we use abiEncode2. And in this case I've made this thing be a 128 bits instead of 256, however, abi.encode is going to try to pad each of its internal values to become 32 bytes. 

![](log10.png)

So when we run abiEncode2 and look at the logs, we're going to see the same result starts at 80 and then goes to e0.

```solidity
    function abiEncodePacked() external {
        bytes32 x40;
        assembly {
            x40 := mload(0x40)
        }
        emit MemoryPointer(x40);
        abi.encodePacked(uint256(5), uint128(19));
        assembly {
            x40 := mload(0x40)
        }
        emit MemoryPointer(x40);
    }
```

abi.encodePacted is interesting because it's going to try to make this (指传给它的参数) as short as possible, so this will be 256 bits and this will be 128 bits, so the entire area it's going to take up is actually going to be smaller. 

![](log11.png)

Let's see that in action. And we look at the logs and now it's gone from 0x80 to 0xd0. So what's the interpretation of that? Well, we needed to add 32 (这32个bytes是用来装长度的，和上一个例子一样) bytes in order to put on the length of what is to follow, so that's going to add another 0x20 over here, which takes us to a0, and then we are going to go from a0 to c0 to accommodate the 256, but then we're going to go from c0 to d0 because we are only taking up 16 bytes. 

<img src="debugmemory2.png" style="zoom:70%;" />

Let's look at the layout in memory. We have the number of bytes that followed. Now, remember last time this was 40 because 64 bytes followed, but now only 96 bytes followed, so now we see a 0x30 over here. So remember, there's a 20 in here which corresponds to the first item being uint256 and then a 10 corresponding to the next item being uint128, so we see our friends, the 5 and the 13 again.

![](howsolidityusememory2.png)

As we see when you call abi.encode or abi.encodePacked, memory will be allocated, similar thing happens for structs and arrays, but they have an explicit **memory** keyword, so this is kind of obvious. Memory is also allocated, of course, when **memory** is set to be the modifier for the function argument.

Whenever a new memory item is declared in solidity, it's simply laid out end to end according to where the free memory pointer is pointing, so after a new object is written, the free memory pointer is set to point ahead of that. So since objects are laid out end to end, it's not possible to grow arrays because arrays might crash into the item in front of it. 

And yul, if you read a variable from solidity that is declared memory that's actually allowed, it's not going to give you the actual item in memory, of course, because you would need the mlode to do that, but what it's going to tell you is this is where the address and memory is. 

A dynamic array will begin with how long the array is so that you know how far down memory this array consists of. So when you are reading a dynamic array, oftentimes you are going to have to add 0x20 in order to skip the length and read the first item in the array. 

```solidity
    event Debug(bytes32, bytes32, bytes32, bytes32);

    function args(uint256[] memory arr) external {
        bytes32 location;
        bytes32 len;
        bytes32 valueAtIndex0;
        bytes32 valueAtIndex1;
        assembly {
            location := arr
            len := mload(arr)
            valueAtIndex0 := mload(add(arr, 0x20))
            valueAtIndex1 := mload(add(arr, 0x40))
            // ...
        }
        emit Debug(location, len, valueAtIndex0, valueAtIndex1);
    }
```

Let's demo this. You will pass in this dynamically sized memory array to this function, get its location, length and the first two values inside of it. We get its location just by loading the array itself. Now this might look a little bit funny because normally you cannot do something like this, uint256 x is equal to array, this of course is not going to compile, but in the yul world this is interpreted as where it is inside of the memory array, to get its length, we would load the first item in the sequence of 32 bytes and then to actually get the values inside the array, we would add to the arrays location 32 bytes because we need to jump over where the length is stored in order to get index 0 and index 1. 

![](args.png)

The function's args and I'm going to pass in 4 and 5 and I will run this and see what we got back.

![](log12.png)

From that very long debug statement. We get four items: one is 80 which is where the array of stored which makes sense, right? Because initially the free memory pointer is initialized to 80, so that's where this sequence of bytes starts, then we get the length, which is 2, we put two items in there and then we get those two items, 4 and 5.