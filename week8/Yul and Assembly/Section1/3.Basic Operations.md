Like most languages, yul has a for loop and if statement, if you are an assembly programmer, this might surprise you because technically assembly does not have for loops in it. But if you want to be technical, yul is not really assembly, it's just something that's barely one level of abstraction above assembly. For loops and if statements are not hard to understand, so I'm not going to go on and on about how for loops and if statements work, I'll just show you how to use them inside of the language.

```solidity
			assembly {
        let halfX := add(div(x, 2), 1)
            
				for { let i := 2 } lt(i, halfX) { i := add(i, 1) } 
        {
          if iszero(mod(x, i)) {
            p := 0
            break
          }
        }
      }
```

Over here, I've combined them into a function that checks if a number is prime, and the simple algorithm for that is to try every number between 2 and half of the number you're testing and then see if the modulus is 0 at any point. Now you'll note that this ( **add(div(x, 2), 1** ) might look a little bit funny because normally we would do something like this ( **uint256 halfX = x / 2 + 1** )

```solidity
			assembly {
        let halfX := add(div(x, 2), 1)
            
				for { let i := 2 } lt(i, halfX) { i := add(i, 1) } 
        {
          if iszero(mod(x, i)) {
            p := 0
            break
          }
        }
      }
```

Now because yul writes out mathematical operations and pretty much everything in the form of a function, there isn't a notion of an order of operations, so the division happens first simply because it's the innermost function, not because it takes precedence over addition. The **for** loop looks the way you would expect it to, here we're initializing 2 where we're starting ( **for { let i := 2 }** ) and here's our stopping condition (**lt(i, halfX)**), and then here we have how we're updating our counter as time goes by ( **{ i := add(i, 1) }** ), and then we have this block of code that executes on each iteration. (for下边的代码块)

```solidity
			assembly {
        let halfX := add(div(x, 2), 1)
        let i := 2
				for {  } lt(i, halfX) {  } 
        {
          if iszero(mod(x, i)) {
            p := 0
            break
          }
          
          i := add(i, 1)
        }
      }
```

Now, technically, you could do this and nothing changes from a logic perspective, but you still need these blocks here because if you remove them then it's not going to compile, over here, we checked the modulus between the number we're testing and the iteration that we're on and if it is zero, so if the modulus is zero, we know that it's not prime and we can stop searching and set our return value to 0 which will get turned into a false. So note that I've declared the variable inside of the return over here (**returns (bool p)**) for convenience and just to save space, I'm going to be doing that for the rest of the functions we look at.

```solidity
			assembly {
        let halfX := add(div(x, 2), 1)
        let i := 2
				for {  } lt(i, halfX) {  } 
        {
          if eq(mod(x, i), 0) {  // line1
            p := 0
            break
          }
          
          i := add(i, 1)
        }
      }
```

Now we could have done something like this, equal this and 0 (line1), that would semantically be the same, but when you're doing yul, this (**iszero(mod(x, i))**) is the more conventional way of doing it. 

```solidity
		function testPrime() external pure {
        require(isPrime(2));
        require(isPrime(3));
        require(!isPrime(4));
        require(!isPrime(15));
        require(isPrime(23));
        require(isPrime(101));
    }
```

Okay, let's test to make sure that this actually works. So I'm going to compile it and then I'm going to deploy it and run it. So over here **testPrime** is just going to loop through all of these candidate numbers to see if they're prime or not, wo we know 15 is not prime and 4 is not prime. This is regular solidity which is why we have the exclamation point operator (感叹号) to negate things, there is no exclamation point operator inside of yul. This is **iszero**, is pretty similar to it and I'll get into negation in the second. Okay, so let's test this works and it doesn't revert and we know that all of the tests pass, but if you want to look at it explicitly, we can see **isPrime** for 101 is true, 7 is also prime and 4 is not prime and neither is 9. 

```solidity
    function isTruthy() external pure returns (uint256 result) {
        result = 2;
        assembly {
            if 2 {
                result := 1
            }
        }

        return result; // returns 1
    }
```

Okay, let's talk about negation now, because yul only have 32 byte words as a type, it doesn't have booleans, so the way you check for truthiness or falseness is a little bit different. Over here, we're going to check if 2 is a true or a false number and if this runs it's going to return 1 because the result will be set to 1. This evaluates the true in yul, let's see that in action, so if we run **isTruthy**, then we get the result of 1.

```solidity
    function isFalsy() external pure returns (uint256 result) {
        result = 1;
        assembly {
            if 0 {
                result := 2
            }
        }

        return result; // returns 1
    }
```

Falsy values in yul are where all of the bits inside of the 32 byte word are zero, so this will be set to 1, because this will not get set, so if I run **isFalsy**, then it's going to be 1, the default value over here because this block was not executed. 

```solidity
    function negation() external pure returns (uint256 result) {
        result = 1;
        assembly {
            if iszero(0) {
                result := 2
            }
        }

        return result; // returns 2
    }
```

So let's look at how we can negate things in yul, over here if we say **iszero**, if 0 **iszero** that's going to be true. And this ( **iszero(0)** ) is going to return the 32 byte word with 1, with the bit at the end set to 1, so this will be set to true, so that did get negated, the 2 was set. 

```solidity
    function unsafe1NegationPart1() external pure returns (uint256 result) {
        result = 1;
        assembly {
            if not(0) {
                result := 2
            }
        }

        return result; // returns 2
    }
```

Now, generally, if you're trying to negate things inside of yul, you should avoid using the **not** word as opposed to **iszero**. Now, in this situation, things will work as we expect them to, so **not(0)** is a truth value, so this will return true, sorry this comment is false, let's run **unsafe1NegationPart1**, and sure enough it returns true. 

```solidity
    function bitFlip() external pure returns (bytes32 result) {
        assembly {
            result := not(2)
        }
    }
```

Now where that will get interesting is if you take **not** of something that isn't 0, what it simply going to do is just flip all of the bits and you will get a non-zero value back.

![](bitflip.png)

So if we look at **bitFlip** over here, you can see that 2 has a lot of zeros in it which all become Fs, which is where all of the bits are set to 1 except at the very end where d got flipped into 2 (0010 -> 1101), you can do the modular arithmetic in your head and think about why that's true, but that's an exercise for the viewer. 

not是按位取反，并不是0和非0互相转换

```solidity
    function unsafe2NegationPart() external pure returns (uint256 result) {
        result = 1;
        assembly {
            if not(2) {
                result := 2
            }
        }

        return result; // returns 2
    }
```

So now that we know that's true, it's no surprise why this will not behave the way that we expect, this will still evaluate to a true condition and this will be set to 2, so if we look at **unsafe2Negation**, then we get 2 back, but if we were to do **iszero**, then this would be 1 as we expect it to be. 

```solidity
    function max(uint256 x, uint256 y) external pure returns (uint256 maximum) {
        assembly {
            if lt(x, y) {
                maximum := y
            }
            if iszero(lt(x, y)) {
                // there are no else statements
                maximum := x
            }
        }
    }
```

Now continuing with if statements, the other thing that you should know is that they don't have **else**. So if you want to check out different scenarios, you actually need to explicitly check them. Now there are more elegant ways of doing this in yul, but just because I want to focus on if statements on the really simple things for now, I'm not going to discuss that in this particular video, but here is how we would return the maximum of two values.

Over here, I've declared the maximum variable inside of the return statement and we will either set maximum to be y, if x is less than y, and if the inverse is true, we're going to set it the other way. Now, this is not the best way to do it, but this illustrates what we can do for now, the point that I'm trying to make here is that **there's no else**. 

So let's find the max of 3 and 5 and we get 5 back, over here these **lt** ( less than ) operators and **iszero** is hopefully pretty intuitive. In fact, there is a pretty clear mapping between what you see here and what you're used to seeing in solidity or other C-like languages. So a && b is written as and(a, b), a || b is written as or(a, b) and a ^ b is written this way. 

```solidity
    // The rest:
    /*
        | solidity | YUL       |
        +----------+-----------+
        | a && b   | and(a, b) |
        +----------+-----------+
        | a || b   | or(a, b)  |
        +----------+-----------+
        | a ^ b    | xor(a, b) |
        +----------+-----------+
        | a + b    | add(a, b) |
        +----------+-----------+
        | a - b    | sub(a, b) |
        +----------+-----------+
        | a * b    | mul(a, b) |
        +----------+-----------+
        | a / b    | div(a, b) |
        +----------+-----------+
        | a % b    | mod(a, b) |
        +----------+-----------+
        | a >> b   | shr(b, a) |
        +----------+-----------+
        | a << b   | shl(b, a) |
        +----------------------+
    */
```

Now, these are all bitwise operations, in a language like JavaScript where it can be a little bit unclear what the return type you're going to get when you do an operation like this. These are always bitwise operations and operations on 32 byte words. So this is going to AND the bits together or and so will this, and same thing for all of the addition, subtraction, multiplication and division, now remember, in solidity 0.8, you get overflow protection, of course, you don't get overflow protection in yul, so you need to watch out for that.

Same thing for the modulus and with shifting bits. Now if you are curious, the complete list of everything that you can do is actually in this [table](https://docs.soliditylang.org/en/latest/yul.html#evm-dialect) and you will see that the vocabulary in yul is actually not that big. So knowing all of the right keywords is actually pretty straightforward. Knowing how to manage memory and calldata and storage is tricky because solidity abstracts that away from yul, so we'll be spending a lot of time talking about those things in separate videos.