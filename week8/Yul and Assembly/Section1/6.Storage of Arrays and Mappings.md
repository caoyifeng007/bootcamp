Now we're going to discuss how Solidity computes the storage slot for arrays and mappings. 

If an array is fixed, it's going to behave pretty conventionally, it's no different than if you had done something like uint256 a, uint256 b uint256 c, uint256 d, so the 0, 1, 2 and 3 index would correspond to these variables over here as I will demonstrate below.

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.17;

contract StorageComplex {
    uint256[3] fixedArray;
    
    constructor() {
        fixedArray = [99, 999, 9999];
    }
    
    function fixedArrayView(uint256 index) external view returns (uint256 ret) {
        assembly {
            ret := sload(add(fixedArray.slot, index))
        }
    }
    
}
```

So I have the constructor setting fixed array to 99, 999 and so on, and if we are to load the fixed array and look at its slot and simply add the index to it, we're going to be able to pull those values out. 

![](https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section1/fixedarrayview.png)

So let's do that, our function is fixedArrayView and if we look at 0, we get 99, 1 we get 999 and 2 we get 99999. 

```solidity
contract StorageComplex {
    uint256[] bigArray;
    
    constructor() {
        bigArray = [10, 20, 30, 40]; // line1
    }
    
    function bigArrayLength() external view returns (uint256 ret) {
        assembly {
            ret := sload(bigArray.slot)
        }
    }
    
}
```

What about for arrays that have dynamic length? If you were to look inside of this storage slot (指**bigArray**) which is what we do with this function over here, **bigArrayLength()** that's going to look inside of **bigArray** and get its slot, as you can kind of infer from the name of the function, if we look at **bigArrayLength**, that's going to be 3 which is what we expect because we loaded three items in there (line1). If we were to add a fourth one (添加了40), then we would see that this becomes 4, this (指4，即动态数组的长度) is what is being stored inside of this storage slot, so **we can get the length from bigArray like that**.

But how do we get the items that are inside of it? Now, they're not going to be stored sequentially down the slots like they would with a fixed array, and the reason for that is that this could overrun and then crash into something down below it (因为是动态数组，如果在当前slot开始存储item，随着item的增加，会影响并覆盖后边的slot，所以只在当前的这个slot只存数组的长度，然后在另外一个由keccak256生成的值的地方开始存数组中的item). 

```solidity
function readBigArrayLocation(uint256 index)
    external
    view
    returns (uint256 ret)
{
    uint256 slot;
    
    assembly {
    	slot := bigArray.slot
    }
    
    bytes32 location = keccak256(abi.encode(slot));

    assembly {
    	ret := sload(add(location, index))
    }
}
```

So what Solidity does instead is quite interesting, what it's going to do? It's going to take the slot of where that array is and then it's going to take the keccak256 of it, that gives you the location, and then as you want to read items sequentially on the array, then you would just simply add the index, so this is a hash plus 1, plus 2, plus 3 of that hash. 

So why do they do that? Well, when you take a hash of a number, then it's just going to land in this extremely enormous 256 bit space, chances are you're not going to be able to add enough numbers to that hash in order for it to crash into something else. Because 2^256 is a mind bogglingly huge number and that's Solidity's methodology to be able to grow arrays in an unlimited way and not crash into any other storage slots. (个人理解：这里应该是利用了哈希函数的特性，对两个不同的slot值，那么得到的哈希值一定是不一样的，且两个哈希值相差巨大，然后一个哈希值作为一个动态数组的起始slot，只要这个数组长度不够大，那么就不会和别的哈希值（其他数组的起始位置或者mapping的存储位置）碰撞)

```solidity
bytes32 location = keccak256(abi.encode(slot));

assembly {
	ret := sload(add(location, index))
}
```

Let's see that in action. So if we read the **bigArray**, we get a value of 10 back, and how is that being read? That's just looking inside the **keccak256** of the slot of this array, and then as we're changing the index here, we're just adding to the ~~index~~ hash as mentioned earlier which is a 256 bit number, these values match what we have over here. (动态数组是在slot 1的位置上，hash(1)就是第一个item，+0，+1，+2分别的对应的第一个值、第二个值、第三个值)

```solidity
contract StorageComplex {
    uint8[] smallArray;
    
    constructor() {
        smallArray = [1, 2, 3];
    }
    
    function readSmallArray() external view returns (uint256 ret) {
        assembly {
            ret := sload(smallArray.slot)
        }
    }
    
}
```

Okay, what about **smallArray**? Well, in storage, Solidity tries to pack variables, so if we read smallArray, we are going to get 3 back which is what we expect. (这里的big和small是指，big是数组存的是256位的数，small是存8位的数)

```solidity
function readSmallArrayLocation(uint256 index)
    external
    view
    returns (bytes32 ret) // 这里返回值类型为bytes32
{
    uint256 slot;
    
    assembly {
    	slot := smallArray.slot
    }
    bytes32 location = keccak256(abi.encode(slot));

    assembly {
    	ret := sload(add(location, index))
    }
}
```

But if we were to do something similar, **readSmallArrayLocation** (应该是口误，location说成length了) at 0, I've actually configured this to return a bytes 32, so it's a little bit more clear what's going on, and it's going to do the same thing, it's going to find the keccak256 of the slot for that array.

![](https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section1/readsmallarray.png)

And then we're going to see 1, 2 and 3 over here which is exactly what it should be. Now, this is uint8, right? And uint8 is 1 byte, so this is 1 byte and this is another byte and this is another byte. (small array和big array除了存的数据会把pack之外其他都一样)





```solidity
contract StorageComplex {

    mapping(uint256 => uint256) public myMapping;
    
    constructor() {
        myMapping[10] = 5;
        myMapping[11] = 6;
    }
    
    function getMapping(uint256 key) external view returns (uint256 ret) {
        uint256 slot;
        assembly {
            slot := myMapping.slot
        }

		// key和slot一起做哈希
        bytes32 location = keccak256(abi.encode(key, uint256(slot)));

        assembly {
            ret := sload(location)
        }
    }
}
```

Mappings behave quite similar to arrays actually, what an array does is it takes the storage slot, takes the hash of that, and then starts adding 0, 1, 2, 3, 4, in order to get the array. What the mapping does is, it concatenate the key with the storage slot and as you change the key, then that concatenated value changes and that gives you a different storage location.

Let's see that in action, so in **getMapping**, first we get the slot and that will be our slot over here, then it is concatenated with the **key** that we pass in, and then we take the **keccak256** of that, so note that each of these **keccak256** is that we've looked at all take the slot as one of the items that generates the hash that guarantees that the output is going to be unique as long as the hash doesn't collide.

![](https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section1/mymapping.png)

So if we were to concatenate **10** with the slot of this variable (**myMapping**) over here, we expect to get 5 back, so let's put **10** into my mapping and we get **5** back. 

Well, actually put it into the wrong function, this **myMapping** is actually the original thing over there with the **public** getter function put in. (因为**myMapping**这个storage variable是public的，所以自动生成了**同名**的getter，**myMapping()**)

![](https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section1/getmapping.png)

But let's actually do that, the interesting array (这个地方应该是口误，应该是mapping), and look inside of **getMapping** which is over here, now if we put in **10**, we expect to get **5**, and 10 came in here (指abi.encode中的key), we concatenated **10** with the storage slot of **myMapping**, got the location and then did an **sload**.





```solidity
contract StorageComplex {

    mapping(uint256 => mapping(uint256 => uint256)) public nestedMapping;
    
    constructor() {
        nestedMapping[2][4] = 7;
    }
    
    function getNestedMapping() external view returns (uint256 ret) {
        uint256 slot;
        assembly {
            slot := nestedMapping.slot
        }

        bytes32 location = keccak256(  // line1
            abi.encode(uint256(4), keccak256(abi.encode(uint256(2), uint256(slot))))
        );
        assembly {
        	// ret := sload(add(location, 0)
            ret := sload(location)
        }
    }
    
}
```

Nested mappings are just hashes of hashes, so first we start with the slot as usual, and then this 2 is the first key inside of this nested map, then we take the keccak256 of that and then concatenate it with the next key, take the keccak256 of that and then put it in to the sload, this **add** **0** actually doesn't accomplish anything, I'm not sure why I wrote that ( 删掉了**add(, 0)** ), but as you can see, it's not going to change the value.

In the constructor, I've set the nested mapping to be 2 and 4 which is why you saw 2 and 4 this way, so note that the concatenation and hashing goes in a leftward direction, even though the mapping goes to the right. (这里的意思是把line1拉平会发现，从右向左一次结合2和4两个key，然后正常读取nested mapping是从左向右一次读取2和4两个key)

So let's get our nested mapping and it returns 7, oh yeah, we hardcoded the keys just for simplicity, but it's looking at 2 and then it looks at 4, and according to the constructor, 2 and 4 should produce 7.





Now the next thing about concatenating the variables is, you're not restricted to using integers as the hash key, you could use an address, and I'm going to also use this opportunity to do something a little bit more complicated, we'll take an address to a dynamically long array. (前边的例子的mapping都是**mapping(uint256 => uint256)**，为下边这个**mapping(address => uint256[])**做铺垫)

So why is this interesting? Well, there's two things, well, one, we have to store the length of this array somewhere, and then we have to store the actual values.

```solidity
contract StorageComplex {

    mapping(address => uint256[]) public addressToList;
    
    constructor() {
        addressToList[0x5B38Da6a701c568545dCfcB03FcB875f56beddC4] = [
            42,
            1337,
            777
        ];
    }
    
    function lengthOfNestedList() external view returns (uint256 ret) {
        uint256 addressToListSlot;

        assembly {
            addressToListSlot := addressToList.slot
        }

        bytes32 location = keccak256(
            abi.encode(
                address(0x5B38Da6a701c568545dCfcB03FcB875f56beddC4),
                uint256(addressToListSlot)
            )
        );

        assembly {
            ret := sload(location)
        }
    }
}
```

So let's see how we would do it in yul, so first of all, to get its length, we will concatenate the slot with the key, I've just hardcoded this, so I don't have to keep copying and pasting this, and then we take the **keccak256**, so... so far that behaves like a normal mapping, what it's saying is if you just treated this like a mapping that went say, from an address to a uint256, this is going to give you back the length of this array which we are able to see here.

![](https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section1/lengthofnestedarray.png)

The length of the nested array is 3 which it is... like this. (指构造函数中的初始化赋了3个值)

```solidity
function getAddressToList(uint256 index)
    external
    view
    returns (uint256 ret)
{
    uint256 slot;
    assembly {
    	slot := addressToList.slot
    }

    bytes32 location = keccak256(
        abi.encode(
            keccak256(
                abi.encode(
                    address(0x5B38Da6a701c568545dCfcB03FcB875f56beddC4),
                    uint256(slot)
                )
            )
        )
    );
    assembly {
    	ret := sload(add(location, index))
    }
}
```

Now, how do we get the values out? Well, what we're going to have to do in that situation is take the **keccak256** of that again, so originally we concatenated the slot with the key and then took the keccak, but if we just take the keccak one more time on that same thing (指**lengthOfNestedList**函数中的location，对其再做一遍**keccak256**), then that will give us the location (这个location就是mapping中硬编码的这个地址所对应的动态数组中的元素开始存储的地方), and just like the array case, we simply add the index to it, so **getAddressToList**, if we look at 0 is going to give us 42, if we look at 1, 1337 and 2 is 777 which is just what we expect them to be.

Now, writing simply happens in the reverse, I've shown you how to get the storage location that Solidity calculates in order to generate these mappings, you would use the same algorithm to get that storage lot... slot and then do an **sstore** instead. As an exercise you can do something funky, like having a map, pointing to a struct, pointing to a dynamic array and so on, but the principles will remain the same.