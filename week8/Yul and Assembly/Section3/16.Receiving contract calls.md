We're now going to write a contract that's almost entirely in yul that will imitate a Solidity contract. In the next video, we will write the contract 100% in yul but I don't want to throw too much information at you because we still have quite a bit to cover here. 

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.17;

contract CallDemo {
    ICalldataDemo public target;

    constructor(ICalldataDemo _a) {
        target = _a;
    }

    function callGet2() external view returns (uint256) {
        return target.get2();
    }

    function callGet99(uint256 arg) external view returns (uint256) {
        return target.get99(arg);
    }
}
```

So imagine that we have a contract like this, it simply calls another contract whose functions are **callGet2** and **callGet99**

```solidity
interface ICalldataDemo {

    function get2() external view returns (uint256);

    function get99(uint256) external view returns (uint256);
}
```

You are given an interface that looks like this, this is **get2** takes no arguments and returns uint256, **get99** takes an argument and returns uint256, if you were to actually implement the interface in Solidity, this would be trivially easy.

```solidity
contract CalldataDemo {
    fallback() external {
        assembly {
            let cd := calldataload(0) // always loads 32 bytes
            // d2178b0800000000000000000000000000000000000000000000000000000000
            let selector := shr(0xe0, cd) // shift right 224 bits to get last 4 bytes
            // 00000000000000000000000000000000000000000000000000000000d2178b08

            // unlike other languages, switch does not "fall through"
            switch selector
            // get2()
            case 0xd2178b08 {
                returnUint(2)    // line1
            }
            // get99(uint256)
            case 0xba88df04 {
                returnUint(getNotSoSecretValue())
            }
            default {
                revert(0, 0)
            }
        }
    }
}
```

But how would we do it in yul? Well, this is how we would do it: the calling function is going to interact with us as if we were a Solidity contract, which means if it's using the interface properly, the first four bytes inside of the calldata are going to be the function selector, so we will want to hardcode function selectors that match what's in the interface, so this is the function signature of **get2** and **get99**, we take the **keccak256** of that and store the first 4 bytes over here. 

Over here we only have two function selectors but we might have more in a larger contract, so it's more convenient to use a **switch** statement rather than an **if** statement, this is not something we've covered yet, but it's a very handy thing that yul has which Solidity doesn't, it behaves like other languages where it's going to compare the variable against different values that it might be equal to. Unlike other languages, when a match is found, it's not going to try out the other **case** statements which I think is a good design decision. (Solidity没有switch而yul有，c语言中switch case中要break，如果不添加break就会执行下一条case，而yul执行一条case就自动退出了)

You'll note inside of here (line1) we're calling a function which is another thing that we haven't discussed yet, yul allows you to have functions.

```solidity
assembly {
    let cd := calldataload(0) // always loads 32 bytes
    // d2178b0800000000000000000000000000000000000000000000000000000000
    let selector := shr(0xe0, cd) // shift right 224 bits to get last 4 bytes
    // 00000000000000000000000000000000000000000000000000000000d2178b08

    // unlike other languages, switch does not "fall through"
    switch selector
    // get2()
    case 0xd2178b08 {
    	returnUint(2)
    }
    // get99(uint256)
    case 0xba88df04 {
    	returnUint(getNotSoSecretValue())
    }
    default {
    	revert(0, 0)
    }

    function returnUint(v) {
        mstore(0, v)
        return(0, 0x20)
    }
}
```

Let's show what happens when **returnUint** is called, so **returnUint** is a function, notice how it looks very similar to a Solidity function, but because it's inside the assembly block, it's yul function, so it takes an argument which is of course a 32-byte value (指v的类型，yul的类型只有32 bytes), it will store that in 0 and return that, this **return** is going to hand control back to the calling contract and the execution inside of this smart contract, this is not going to return back to the yul code even though the **return** is inside the function and it might look like that. (使用return就直接全部退出了，控制权还给calling contract了)





```solidity
assembly {
    let cd := calldataload(0) // always loads 32 bytes
    // d2178b0800000000000000000000000000000000000000000000000000000000
    let selector := shr(0xe0, cd) // shift right 224 bits to get last 4 bytes
    // 00000000000000000000000000000000000000000000000000000000d2178b08

    // unlike other languages, switch does not "fall through"
    switch selector
    // get2()
    case 0xd2178b08 {
    	returnUint(2)
    }
    // get99(uint256)
    case 0xba88df04 {
    	returnUint(getNotSoSecretValue())
    }
    default {
    	revert(0, 0)
    }

    function getNotSoSecretValue() -> r {
        if lt(calldatasize(), 36) {   // line1
        	revert(0, 0)
    	  }

        let arg1 := calldataload(4)
        if eq(arg1, 8) {
            r := 88
            leave
        }
        r := 99
    }
}
```

The other situation is where someone calls **get99** which also takes an argument, the **return** statement from earlier is going to cause the execution to leave the contract, but you can still return values and stay within the execution of your yul code. (意思是说get2()用了return也能返回值，但是会交还控制权，但是在yul中还是有别的方法不交还控制权的)

In this example, we're returning a named variable called **r**, and **r** is going to be set to one of two values depending on what argument was passed in, but let's not get ahead of ourselves here, what is going to do is load **arg1** from calldata, so it's going to skip the first 4 bytes because that's where the function selector is and it's going to test if it's equal to 8, if so, it's going to set **r** to be equal to 88. 

Now remember, we don't have L statements inside of yul, so to stop the execution inside the function and go back to where we originally were, there's a keyword called **leave**, otherwise we're going to set **r** to be equal to 99 and return that.

Over here (line1), we have to validate that we actually have enough bytes inside of the calldata such that there could be an argument there, we require that there are at least 36 because the first 4 would be the function selector, in the next 32 is the argument that's being passed in, if we have less than that, you could still ask for the argument, but you're going to just get 0 as a result which is not really the expected behavior. 





```solidity
let cd := calldataload(0) // always loads 32 bytes
// d2178b0800000000000000000000000000000000000000000000000000000000

let selector := shr(0xe0, cd) // shift right 224 bits to get last 4 bytes
// 00000000000000000000000000000000000000000000000000000000d2178b08
```

Now, let me talk about what this **shr** over here is doing, **calldataload** is only capable of loading 32 bytes, so when we have the function selector, it's also going to load 28 bytes in front of it. 

```solidity
// unlike other languages, switch does not "fall through"
switch selector
// get2()
case 0xd2178b08 {
	returnUint(2)
}
// get99(uint256)
case 0xba88df04 {
	returnUint(getNotSoSecretValue())
}
default {
	revert(0, 0)
}
```

Now in these **case** statements, we could technically just hardcode this to be 32 bytes and have a bunch of zeros in the least significant bits, but the problem with that is this won't match over here when a function argument is supplied (因为函数要是提供参数的话，function selector后边就不可能是0) because we don't know what argument is going to be to the right of this, so what we do is we load 32 bytes and shift everything in front of those 4 byte selector off, so that they disappear, and then we can match the function selector with the function that we're trying to call, that's what this **shr** 0xe0 is, this 0x0e is 224 bits or 28 bytes which is the exact right amount to put the 4 bytes at the end and shift everything else off. 

```solidity
/**
     To Learn
1. calldataload
2. imitating function selectors
3. switch statement
4. yul functions with arguments
5. functions that return
6. exit from a function without returning
7. validating calldata
**/
```

So let's summarize what we've learned: we've seen that **calldataload** always loads in increments of 32 bytes, and if we're going to imitate a function selector in Solidity, then we're going to want to shift off everything except the 4 bytes and match it against the **switch** statement, to keep our code clean, we can put functions inside of that **switch** statement that are going to execute what we want to, with those functions, it's possible to use the **leave** keyword in order to leave the function without returning the execution back to the calling contract, but if we wanted to return or revert inside those functions, then we can, we just have to remember that **<u>return</u> means something different inside of yul than it does inside of Solidity**, and finally, we shouldn't just trust the calldata, this is, of course, validated for you inside of Solidity, and yul you have to do it yourself. (使用yul，获取calldata中的参数时需要自己手动验证传递来的实参)



<img src="https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section3/calldatademo.png" style="zoom:80%;" />

So let's see that this actually works, over here we have **CalleddataDemo** which is our contract that is mostly yul and it has no visible functions on the outside, so, of course, remix doesn't show anything. (代码在最上边，因为是yul，所以remix就识别不出来function按钮)

<img src="https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section3/calldemo.png" style="zoom:80%;" />

Over here we have **CallDemo**, when we call **get2**, it works as expected, and when we **call99** with 4 and we get 99 back, but if we call it with 8, we'll get 88 back, and that is the expected behavior that we see over here. (用regular Solidity合约去调yul合约，效果也是一样的)