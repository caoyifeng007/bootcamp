Now that we know the kind of **tx.data** that Solidity is expecting, we're ready to see it implemented in yul. (前一节讲解了Solidity对于传递来的数据的解析，即前4个字节是function selector，后边每32个字节是一个参数，这节是讲解在如何yul中构造这个数据，好发起交易)

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.17;

contract OtherContract {

    // "9a884bde": "get21()",
    function get21() external pure returns (uint256) {
        return 21;
    }
}
```

Over here we have this contract with a function called **get21** which simply returns 21, and for the sake of convenience, I've included its <u>function signature</u> over here, our goal is to call this from another contract entirely in yul, for the sake of convenience, I've included the <u>function selector</u> over here.

```solidity
contract ExternalCalls {
    // get21()	0x9a884bde
    // x()		0c55699c
    function externalViewCallNoArgs(address _a)
        external
        view
        returns (uint256)
    {
        assembly {
            mstore(0x00, 0x9a884bde)  // line1
            // 000000000000000000000000000000000000000000000000000000009a884bde
            //                                                         |       |
            //                                                         28      32
            let success := staticcall(gas(), _a, 28, 32, 0x00, 0x20)
            if iszero(success) {
                revert(0, 0)
            }
            return(0x00, 0x20)
        }
    }
}
```

So back to our yul contract, to make the call, what we will want to do is load the function selector into memory and then point to the region (line1), that will be part of our abi call, because our abi call is only going to have 4 bytes in it, then the region we will point to will naturally only have 4 bytes.

Solidity's opcode for making external calls are **staticcall**, **call** and **delegatecall**. Because we are inside of a **view** (用鼠标指了下函数签名上的view关键字) function, we have to use **staticcall** because **staticcall**s do not change the state and if a state change happens, then they revert. 

```solidity
let success := call(gas(), _a, 0, 28, 32, 0x00, 0x20)  // 普通的call比staticcall参数多了个0
```

If we use regular **call**, then... well, we put the wrong number of arguments which I will describe in a second, but if we use regular **call**, we cannot do that, because this "Function cannot be declared as **view** because this expression potentially modifies the state", so **staticcall** ensures that we are not doing a state change and the compiler notices that we are inside of a view function. (因为使用了call，所以函数就不能声明成view了，使用staticcall才可以)

Anyway, let me describe what the arguments to **staticcall** are. The first argument is how much gas you want to pass to that other function, in this situation, what we're doing is passing all of the remaining gas that we have left, but if you wanted to hardcode this amount to a smaller amount, you could do that, and sometimes being flexible with this is useful if you don't trust the receiving contract because the receiving contract might use up all the gas in an attempt to conduct a denial of service (拒绝攻击), but in this case, we're sending the whole thing.

```solidity
mstore(0x00, 0x9a884bde)  // line1
// 000000000000000000000000000000000000000000000000000000009a884bde
//                                                         |       |
//                                                         28      32
let success := staticcall(gas(), _a, 28, 32, 0x00, 0x20)
if iszero(success) {
        revert(0, 0)
}
return(0x00, 0x20)
```

This (**_a**) is the address of the contract we're calling, this (**28, 32**) is the intended **tx.data** which is what we have loaded into memory, and this (**0x00, 0x20**) is the region in memory that we are going to copy the results back to, so when the function returns, it's going to overwrite this area (**0x00, 0x20**这个内存区间) and replace it with the return value, but that's okay, we don't need this anymore (9a884bde是存储在0x00-0x20上的，然后指定了0x00-0x20这个内存区间来接收返回值，这样会将9a884bde覆盖，但是没有关系，9a884bde已经没用了).

We check the **success** value and then return that area in memory where the answer has been written to, and this is obviously the same size as this (0x00-0x20这个内存区间是32个字节，和函数的返回值类型**uint256**相匹配).

<img src="https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section3/externalviewcallnoarg.png" style="zoom:80%;" />

So let's see this in action, going to deploy **ExternalCalls** and the **OtherContract**, so we'll copy the **OtherContract**'s address and this is external, **externalViewCallNoArgs**, so that's over here and I'm calling this and I get 21 back.







```solidity
contract OtherContract {

    // "73712595": "revertWith999()",
    function revertWith999() external pure returns (uint256) {
        assembly {
            mstore(0x00, 999)
            revert(0x00, 0x20)
        }
    }
}

contract ExternalCalls {

    function getViaRevert(address _a) external view returns (uint256) {
        assembly {
            mstore(0x00, 0x73712595)
            pop(staticcall(gas(), _a, 28, 32, 0x00, 0x20))
            return(0x00, 0x20)
        }
    }
}
```

We mentioned in an earlier video that even when a function reverts, it's able to return values, let's see that in action here. We're making a **staticcall** to this function, and what it's going to do is store a 999 into the memory and then **revert** with it, so this is going to behave the same way as a **return**, the only difference is that the **success** (指上一个例子中的返回值success) is going to be **0** rather than **1**, but because we know that in advance, we're just going to pop it off to get rid of it, and then we will return what our **staticcall** returned. Note that when a function reverts or succeeds, it's going to be written to the same area that you pre-allocate for it. (这段就是讲，尽管revert了，但是还是可以像return一样返回值，revertWith999返回了999，被存到内存的0x00-0x20内存区间上，然后被返回就是999)

<img src="https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section3/getviarevert.png" style="zoom:80%;" />

So we will copy the target address and **getViaRevert** and we get 999 back. 





```solidity
contract OtherContract {
    // "196e6d84": "multiply(uint128,uint16)",
    function multiply(uint128 _x, uint16 _y) external pure returns (uint256) {
        return _x * _y;
    }
}

contract ExternalCalls {
    function callMultiply(address _a) external view returns (uint256 result) {
        assembly {
            let mptr := mload(0x40)
            let oldMptr := mptr
            mstore(mptr, 0x196e6d84)
            mstore(add(mptr, 0x20), 3)
            mstore(add(mptr, 0x40), 11)
            
            //  advance the memory pointer 3 x 32 bytes
            mstore(0x40, add(mptr, 0x60))   // line1
            //  00000000000000000000000000000000000000000000000000000000196e6d84
            //  0000000000000000000000000000000000000000000000000000000000000003
            //  000000000000000000000000000000000000000000000000000000000000000b
            //                                   9c                e0
            let success := staticcall(gas(), _a, add(oldMptr, 28), mload(0x40), 0x00, 0x20)
            if iszero(success) {
                revert(0, 0)
            }

            result := mload(0x00)
        }
    }
}
```

If you want to call a function that takes arguments, we just need to make our abi encoding proper, as we saw in the previous video, for a function that takes two **uint256** arguments or something smaller than that, we just stack 32 bytes on top of each other (指了指3和b). (前两个例子一个是直接返回21，一个是revert的同时返回999，都不需要传参，这个例子开始需要向其传递参数了)

So we are going to call this function (指**multiply**) over here and it takes two arguments again, note that these are not **uint256**, but abi encoding still specifies that this data be supplied with 32 bytes and we will just return the product of these two numbers. (这段是说**multiply**的形参x和y，虽然一个是uint128，一个是uint16，但是它俩都会被abi.encode补齐到32个字节)

So we will load in the function selector and the two arguments, the region in memory we are supplying is going to be a little bit different, because this is more than 64 bytes, we can't stack this into the **scratch space** (意思是说要想调用**multiply**返回函数，需要3个32个字节的参数，第一个是function selector，后两个是参数，那么就是96个字节了，memory的scratch space只有64个字节，所以存不下，只能从free memory pointer开始往后存)

So what we did is we loaded it at the free memory pointer, the free memory pointer would point at the beginning of the first set of bytes that we wrote, so we have to add **28** to it, so that we're only looking at the first 4 bytes inside of the first 32 byte slot. (**oldMptr**是0x80，加了28之后，内存区域从selector的19开始，然后从19一直到最后的0b，**196e6d84**就是他说的first 4 bytes)

```solidity
// identical to  call(g, a, 0, in, insize, out, outsize) but do not allow state modifications
staticcall(g, a, in, insize, out, outsize)

// call contract at address a with input mem[in…(in+insize)) 
// providing g gas and v wei and output area mem[out…(out+outsize)) returning 0 on error (eg. out of gas) and 1 on success
call(g, a, v, in, insize, out, outsize)
```

And then because we updated the memory pointer (指line1这行更新pointer) after, to point to... after where we wrote our data (指上边三行mstore), we'll just load the memory pointer back (指staticcall中的第4个参数**mload(0x40)**) so that we're looking in this region (指从**add(oldMptr, 28)** 到 **mload(0x40)**这一段左开右闭的内存区间), and as usual we will specify the first 32 bytes to receive the return data. (指从0x00-0x20这32个字节存放staticcall的返回值)

<img src="https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section3/callmultiply.png" style="zoom:80%;" />

And when we **callMultiply**, we see 33 which is indeed the product of 3 and 11.





```solidity
contract OtherContract {

    // "0c55699c": "x()"
    uint256 public x;

    // "4018d9aa": "setX(uint256)"
    function setX(uint256 _x) external {
        x = _x;
    }
}

contract ExternalCalls {

    // setX
    function externalStateChangingCall(address _a) external {
        assembly {
            mstore(0x00, 0x4018d9aa)
            mstore(0x20, 999)
            // memory now looks like this
            // 0x000000000000000000000000000000000000000000000000000000004018d9aa
            // 0x00000000000000000000000000000000000000000000000000000000000003e7
            // let success := call(gas(), _a, 0			 , 28, add(28, 32), 0x00, 0x00)     // line1
            // let success := call(gas(), _a, 1 ether, 28, add(28, 32), 0x00, 0x00)     // line2
            let success := call(gas(), _a, callvalue(), 28, add(28, 32), 0x00, 0x00)
            if iszero(success) {
                revert(0, 0)
            }
        }
    }
}
```

We've been using **staticcall** in the functions previous to this, now let's look at regular **call**. The primary difference between them is that there is an additional argument called **callvalue()** which is what you use to forward the Ethereum (应该是指ether) that you are receiving as part of the transaction. (上一个例子是传递参数，但不改变state变量，所以使用**staticcall**，这个例子传递参数并改变state变量，所以使用**call**)

Now, in this case, because this function is not **payable**, this **callvalue()** is always going to be **0**, so we could just make it 0 (line1) in the function would be more efficient, but if indeed we had something that was **payable**, and we wanted to send all of the ether we had received or maybe a fixed amount, we could do that (line2), but other than that, the arguments are the same because we're not receiving a value back, we just specify the receiving area to be 0 (指最后两个参数0x00, 0x00，意思是说除了第三个参数之外其他参数都一样，因为**setX**没有返回值，所以接收返回值的内存区域设置为0x00和0x00就行).

<img src="https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section3/externalStateChangingCall.png" style="zoom:60%;" /><img src="https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section3/getx.png" style="zoom:80%;" />

This is going to call this function over here, which is going to set x and because x is public, we'll be able to see directly what happened, so we conducted the transaction and when we get x, it's 999 as we set it to be.





```solidity
contract OtherContract {

    // "7c70b4db": "variableReturnLength(uint256)",
    function variableReturnLength(uint256 len)
        external
        pure
        returns (bytes memory)
    {
        bytes memory ret = new bytes(len);
        for (uint256 i = 0; i < ret.length; i++) {
            ret[i] = 0xab;
        }
        return ret;
    }
}

contract ExternalCalls {

    function unknownReturnSize(address _a, uint256 amount)
        external
        view
        returns (bytes memory)
    {
        assembly {
            mstore(0x00, 0x7c70b4db)
            mstore(0x20, amount)

            let success := staticcall(gas(), _a, 28, add(28, 32), 0x00, 0x00)
            if iszero(success) {
                revert(0, 0)
            }

            returndatacopy(0, 0, returndatasize())  // line1
            return(0, returndatasize())
        }
    }
}
```

```solidity
//  copy s bytes from returndata at position f to mem at position t
returndatacopy(t, f, s)
```

An additional thing worth calling out explicitly is that there are situations where we don't know how big the return value will be, so we've been here to for... loading everything into the first 32 bytes (指最后两个参数). What if the function we are calling returns a variable length of data, like it returns an array or something? In that case, we would use something that looks like this (line1), **returndatasize()** tells you how large the return data was, so it ignores what's you've... what you've allocated to receive (staticcall中最后的两个参数**0x00**, **0x00**被忽略) and just tells you what was the length of the data that was returned back, **returndatacopy()** is going to **copy a region from the return data into your memory**, so it's saying into my memory slot 0 (第一个参数0), copy the return data from 0 (第二个参数0) all the way up until the total size, so you could copy less than the return data, but in this case we're trying to copy the whole thing.

```solidity
// "7c70b4db": "variableReturnLength(uint256)",
function variableReturnLength(uint256 len)
    external
    pure
    returns (bytes memory)
{
    bytes memory ret = new bytes(len);
    for (uint256 i = 0; i < ret.length; i++) {
    	ret[i] = 0xab;
    }
    return ret;
}
```

So let's see what we're calling, what this does is, it simply creates an array that as many bytes long as the argument (指len) that is applied to it, and then fills it with hex **ab**

<img src="https://gitee.com/elvinsj/bootcamp/raw/main/week8/Yul%20and%20Assembly/Section3/unknownreturnsize.png" style="zoom:80%;" />

Let's see that in action, so we have our **unknownReturnSize** over here and let's supply 3, we get 3 bytes back, let's apply 2, we get 2 bytes back, let's apply 10, we get 10 bytes back. (演示了下分别获取0xababab, 0xabab的操作，即接收动态的函数返回值)



Okay, the last thing that we're going to look at is **delegatecall**, this is really only ever used in the context of proxies, and I'm just going to look at the [OpenZeppelin implementation](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/proxy/Proxy.sol#L22) over here, and it should be pretty easy to understand what's going on now. (下边的OpenZeppelin的实现)

```solidity
function _delegate(address implementation) internal virtual {
    assembly {
        // Copy msg.data. We take full control of memory in this inline assembly
        // block because it will not return to Solidity code. We overwrite the
        // Solidity scratch pad at memory position 0.
        calldatacopy(0, 0, calldatasize())  // line1

        // Call the implementation.
        // out and outsize are 0 because we don't know the size yet.
        let result := delegatecall(gas(), implementation, 0, calldatasize(), 0, 0)

        // Copy the returned data.
        returndatacopy(0, 0, returndatasize())  // line2

        switch result   // line2
        // delegatecall returns 0 on error.
        case 0 {
        	revert(0, returndatasize())
        }
        default {
        	return(0, returndatasize())
        }
    }
}
```

Let's ignore this calldata thing (line1) for now, and focus on what happens after the call. During a **delegatecall** you don't know what function you're going to call and therefore you don't know the size of its return value, so they use the pattern we just looked at over here, and based off of the **result** (line2), they're going to check if the recipient of the **delegatecall** reverted (指delegatecall的返回值result是不是0), then they will revert with the data that it returned, otherwise return with the data that it returned, because remember, both **revert** and **return** return the area inside of memory that they specify.

And in the **delegatecall** they have hardcoded **msg.value** to be **0**. You can kind of guess what this (line1) is doing based off of what you saw here (line2), we're going to talk about calldata soon, but over here (line1) they are simply copying the entirety of the calldata into memory, and then saying from **0** to the **calldatasize()**, passed that onto the function that we are calling. You'll note that **this is not memory safe at all in either case, because calldata could be arbitrarily long and overwrite those 64 bytes**, but they know that in the context of this function is being used the transaction will end here, and the memory will be erased anyway.





```solidity
// exercise for the reader #1
function multipleVariableLength(
    uint256[] calldata data1,
    uint256[] calldata data2
) external pure returns (bool) {
    require(data1.length == data2.length, "invalid");

    // this is often better done with a hash function, but we want to enforce
    // the array is proper for this test
    for (uint256 i = 0; i < data1.length; i++) {
        if (data1[i] != data2[i]) {
        	return false;
        }
    }
    return true;
}

// exercise for the reader #2
function multipleVariableLength2(
    uint256 max,
    uint256[] calldata data1,
    uint256[] calldata data2
) external pure returns (bool) {
    require(data1.length < max, "data1 too long");
    require(data2.length < max, "data2 too long");

    for (uint256 i = 0; i < max; i++) {
        if (data1[i] != data2[i]) {
        	return false;
        }
    }
    return true;
}
```

As an exercise for the reader, I want you to think about how you would in abi encode, when you have a function that takes variable length arguments and specifically more than one of them, in this, in this exercise (**multipleVariableLength**) we have 2 arrays and in this exercise (**multipleVariableLength2**) we have 1 integer followed by 2 arrays.

https://docs.soliditylang.org/en/latest/abi-spec.html#abi

All of the documentation for <u>abi encoding</u> is supplied in the link over here, but you actually already know enough for how to do that because we've dealt with variable length before in our section about memory.