Did you know that there is a hack to forcefully send Ether to any contract, even if the receiving contract does not have any payable fallback or payable function? In this video, I will explain how this can be done and also show you how to protect your contract from this hack.

[#Solidity](https://www.youtube.com/hashtag/solidity) [#selfdestruct](https://www.youtube.com/hashtag/selfdestruct) [#hack](https://www.youtube.com/hashtag/hack)

Code: [https://solidity-by-example.org/hacks...](https://www.youtube.com/redirect?event=video_description&redir_token=QUFFLUhqbkl2YXJyd1BQSVphQ0JQV0JWNXV5MjZFUzVhUXxBQ3Jtc0tuUVpoR2doY0Z0aWZhdHpUM3dabW1NMTltOHhzTGdjMjZaTXlEN05LWWw5bmI3SDUxVHNBZGVoTUJGWkhKSWI4eEdBRlRVY3lBTG5fd3BhNHE0VHQ2b2Z2SmZZUV9RN21EcGF6cjVadm9KZXplNGNBdw&q=https%3A%2F%2Fsolidity-by-example.org%2Fhacks%2Fself-destruct%2F&v=cODYglsn3bs) 

References [https://github.com/ethereumbook/ether...](https://www.youtube.com/redirect?event=video_description&redir_token=QUFFLUhqbmM1RzAyRlA5azhLUG00aXcwazBFTmNYSm5Td3xBQ3Jtc0tta2dTTTVna2JERF9LRm1vYU9WdDhuYXYxbHZKX0JFeUJMTDhJcVg4elIwNDN5NzN0T2s0OEY1SGNYYlVQWVlVN1ZkQ3ROY0NjOGlobWlnaE1NdEJWaHBGZXZIbzJYNjhpeGUwRzZaZmVIRnhUNW56OA&q=https%3A%2F%2Fgithub.com%2Fethereumbook%2Fethereumbook%2Fblob%2Fdevelop%2F09smart-contracts-security.asciidoc&v=cODYglsn3bs)  [https://solidity-05.ethernaut.openzep...](https://www.youtube.com/redirect?event=video_description&redir_token=QUFFLUhqbjFoVDA4TVR6TWdYZzNldDJ4YlpwRUIwcF9aUXxBQ3Jtc0tueU5fbkhmZGVNRWV6OXpXS0hEOExjOTJUMV9ld21JbjFTclpCYzlOZmVuU2cxWXhTT0p4NVlCLVNUWkNsN1dTMnEzN0lEc3ZlTzgwMGtsS0lXQzRHUmhMUFdrd3lmTmhEb3lCRFVTZ3hERHhmVDlkQQ&q=https%3A%2F%2Fsolidity-05.ethernaut.openzeppelin.com%2F&v=cODYglsn3bs) 



Over here, I have a contract called Foo, and this contract cannot receive any ether, since it doesn't have any payable fallback function or a payable constructor or payable function. But when we check the balance of this contract, you can see here that this contract has 1 ether. So how was this contract able to receive 1 ether even though it doesn't have any payable function? So in this video I'm gonna show you how to forcefully send ether to another contract using selfdestruct, and then we are code an example. And lastly, we'll talk about how to protect your contract from this hack.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.6.0;

/*
Forcing Ether with selfdestruct
Code
Preventative techniques
*/

contract Foo {
    function getBalance() public view returns (uint) {
        return address(this).balance;
    }
}

contract Bar {
    function kill(address payable addr) public payable {
        selfdestruct(addr);
    }
}
```

Alright, so, first, let's talk about how this contract Foo was able to receive either using selfdestruct. What does the function selfsdestruct do? 

https://docs.soliditylang.org/en/latest/introduction-to-smart-contracts.html?highlight=selfdestruct#deactivate-and-self-destruct (solidity官网对self-destruct的定义)

Basically, selfdestruct deletes the contract from the blockchain. Here we have contract Bar when the function kill is called, it calls selfdestruct and this will delete this contract bar from the blockchain. The function selfdestruct requires one input, here you need to pass in an input of type address, and this will send ether stored in this contract to the address that is given here, regardless of whether the contract at this address has a payable fallback function or not.

So this means that we can forcefully send ether to any address by calling selfdestruct and then setting the address here, and this is how contract Foo above has 1 ether even though it doesn't have any payable function, that's how you forcefully send eater to any contract, even contracts without payable functions, and using this idea, I want to show you a little bit more sophisticated example.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.6.0;

contract EtherGame {
    uint public targetAmount = 7 ether;
    address public winner;

    function deposit() public payable {
        require(msg.value == 1 ether, "You can only send 1 ether");

        uint balance = address(this).balance;
        require(balance <= targetAmount, "Game is over");

        if (balance == targetAmount) {
            winner = msg.sender;
        }
    }

    function claimReward() public {
        require(msg.sender == winner, "Not winner");

        (bool sent, ) = msg.sender.call{value: address(this).balance}("");
        require(sent, "Failed to send Ether");
    }

    function getBalance() public view returns (uint) {
        return address(this).balance;
    }
}
```

EtherGame is a contract where the seventh person to deposit 1 ether becomes the winner and the winner can claim the 7 ether stored in this contract. To play this game, the player will call the function deposit, they have to send 1 ether. If the current balance of ether stored in this country is greater than 7 ether then the game is over.

So after 7 ether no one should be able to send more ether. If the current balance is equal to 7 ehter then we set the winner to msg dot sender, in other words here we're saying that, if msg dot sender is the seventh person to deposit 1 ether, then that person becomes the winner. In the winner of this game can claim the rewards by calling the function claimReward. This will send all the ether stored in this contract to the winner, that is how this EtherGame contract works.

Let's now write a contract that can break this game. The basic idea is to use selfdestruct so that the balance exceeds the targetAmount of 7 ether. By doing so, the balance will never equal the targetAmount and the winner will never be set. No one will be able to call the function claimReward and all of the ethers from the players are stuck in this contract. That's the basic idea of how this contract can be broken.

```solidity
contract Attack {
    function attack(address payable target) public payable {
        selfdestruct(target);
    }
}
```

Let's now write this in code. The contract attack will have a single function named attack, and this is the function that we're going to be calling in order to break contract above, for the input will pass in the target address and then declare it as payable. The reason that the target address is payable is because the address that we pass in to selfdestruct must be a payable address, we'll send ether to this function and then have that ether forcefully sent to the contract above, so here we will declare it as payable, inside the function, will call selfdestruct and then pass in our target address. This completes the contract that's going to break the EtherGame contract above.

Here, I've deployed two contracts EtherGame and the contract that's going to be attacking EitherGame. We will say that account1 is Alice, account2 is Bob and account3 is Eve. So, first of all, let's say that Eve is going to play this game, she's gonna deposit 1 ether, so she calls deposit, Bob also wants to join game. So he also deposits 1 ether, at this point the contract has 2 ether2. And Alice and Bob are trying to become the seventh person to store 1 ether become the winner and claim the seven ethers.

Now Eve is not a nice person and she decides to break this game. She knows that there are 2 ethers in this contract, so if she forcefully sends 5 ethers at once, then she can break the game, and this is because, in order for players to become the winner, they'll have to call the function deposit and also send 1 ether. But after Eve sends 5 ethers, the balance will be 7 ether. So the balance here will be greater than 7 ether since Eve forced it to be 7 ethers. And when a player sends another ether the balance will be greater than 7. This means that the balance will always be greater than the target amount and the function is going to fail here, so that the winner is never set.

Switching the account to Eve, she's gonna send 5 ether and then called attack function and the target will be the EtherGame contract. You can see here that the attack was successful. Let's check the balance of the EtherGame contract, so I'm gonna call getBalance and it has 7 ether which is the winning amount, so who's the winner? The winner is zero address, in other words, the winner was never set and no one will be able to become the future winner. And just show you this. Let's say that Alice tries to send 1 ether by calling deposit, and you can see here that the transaction fail with "Game is over", in other words, the game is over, but we have no winner. That's how you break a game and has 7 ethers stuck in the contract.

```solidity
contract EtherGame {
    uint public targetAmount = 7 ether;
    address public winner;
    uint public balance;

    function deposit() public payable {
        require(msg.value == 1 ether, "You can only send 1 ether");

        // old
        // uint balance = address(this).balance;
        // new
        balance += msg.value;
        require(balance <= targetAmount, "Game is over");

        if (balance == targetAmount) {
            winner = msg.sender;
        }
    }
    ...
}
```

Let's now talk about how to protect this contract from such an attack, what you have to do is not rely on a address this dot balance to get the current balance of ethers sent from all players. So we'll declare a state variable called **balance** and when the player sends the 1 ether will update the balance. Attackers can still forcefully send ethers to this contract, but they won't be able to update the balance state variable unless they play the game call a function deposit and send 1 ether. This will guarantee that the balance state variable is always less than or equal to the targetAmount, and at some point the balance will equal to the targetAmount and the winner will be set.

So that's how you put out your contract from getting broken by an attacker that forcefully sends ether.