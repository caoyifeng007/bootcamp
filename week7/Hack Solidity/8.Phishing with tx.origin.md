Phishing is a cyber attack where the attacker disguises as a trusty-worthy entity and tricks an user into doing something harmful. Watch this video to learn how tx.origin can be vulnerable to phishing. 

[#Solidity](https://www.youtube.com/hashtag/solidity) [#phishing](https://www.youtube.com/hashtag/phishing) [#hack](https://www.youtube.com/hashtag/hack) [#txorigin](https://www.youtube.com/hashtag/txorigin) 

Code: [https://solidity-by-example.org/hacks...](https://www.youtube.com/redirect?event=video_description&redir_token=QUFFLUhqbWpnaUFXN1FYQjFOeHQ0aXQ3Q3p4RVA5MkQwd3xBQ3Jtc0tsRVJuME9scURvNjVQSmctN0JQU1N1TUowdjZqbFFBTGRGemdSa3NHZy1iMDlGWllnUWdfYW5LNzl2TVZJZ25sNGlGNjBTbnpHVklWXzIzVm9mVTdlUWczQWloSWdOU0tpTk9qYXh3aHc3bkR1UTA4aw&q=https%3A%2F%2Fsolidity-by-example.org%2Fhacks%2Fphishing-with-tx-origin%2F&v=mk4wDlVB4ro) 

References [https://github.com/ethereumbook/ether...](https://www.youtube.com/redirect?event=video_description&redir_token=QUFFLUhqazZ2eW9BRUptVERoaFhsN0lkUklFSURmc0NBd3xBQ3Jtc0tuOW4wU1RVTTBjV2RBeVZxMFJ0MlZQWjE3N3VHVjBqaUNYSWJXRzRsWTRjNEpvZEhLV3R2MDZrX29idEMwU2FPek5YX1ZhUkVXc3kwMlJZQVVRaXNyaTlXV3ZtaU9ERzRyWHRHOEp2dlV2eHBpcGFBZw&q=https%3A%2F%2Fgithub.com%2Fethereumbook%2Fethereumbook%2Fblob%2Fdevelop%2F09smart-contracts-security.asciidoc&v=mk4wDlVB4ro) [https://solidity-05.ethernaut.openzep...](https://www.youtube.com/redirect?event=video_description&redir_token=QUFFLUhqbkFPTEVaVXZmWmpCQ3lRdTNxVlItanZydFo5d3xBQ3Jtc0tuNGppWEdOYlVNSTREQllPMnE4T2lDbndQT2VZa3JWekFrWm1WbXh1Njd0azZHd1FtRS1XSXFKdDNEcW9IVkk0Z0FtczBMQWdKNHhFN1E3Y0EyazV2c0pFelZacUpBX3drOEFiQ2xWc3V5ZE16ZlZRUQ&q=https%3A%2F%2Fsolidity-05.ethernaut.openzeppelin.com%2F&v=mk4wDlVB4ro)



Phishing is a cyber attack by which an attacker disguises as a trustworthy entity and deceives a user into doing something they would not want to do otherwise. For example, an attacker can scam a user into sending all of the ether to the attacker's address. So in this video I'm going to explain how tx.origin is vulnerable to phishing. First of all, what is tx.origin, let's say that Alice calls contract A, and contract A calls contract B, inside contract B msg.sender will be equal to contract A, however tx.origin will be equal to Alice. In other words, tx.origin is the address where the transaction was originated. In this case, Alice created the transaction, so tx.origin will be equal to Alice.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.6.0;

contract Wallet {
    address public owner;

    constructor() public {
        owner = msg.sender;
    }

    function deposit() public payable {}

    function transfer(address payable _to, uint _amount) public {
        require(tx.origin == owner, "Not owner");

        (bool sent, ) = _to.call{value: _amount}("");
        require(sent, "Failed to send Ether");
    }

    function getBalance() public view returns (uint) {
        return address(this).balance;
    }
}
```

Let's now look at a contract that uses tx.origin. Here we have a contract called wallet and it is a simple smart contract where anyone can deposit ether into this contract, but only the owner will be able to spend ether from this contract by calling the function transfer. Now, let's take a look at how this function checks that the caller is equal to owner, and the line where it checks that the caller is owner is here on line 28 and it checks that tx.origin is equal to the owner state variable and this owner state variable is set to msg.sender during construction, now let's say that Alice deploys this contract, so the owner's state variable will be equal to Alice and she'll be able to withdraw ether from this contract by calling the function **transfer** when she does call this function it will first check that tx.origin is equal to Alice.

Now recall that tx.origin points to the address that created this transaction, in this case since Alice called this function tx.origin will be equal to Alice, so this check will pass and she'll be able to send ether, but what happens if Eve calls this function? Well, when she does, tx.origin will be equal to Eve and the owner will be equal to Alice, so this check will fail and Eve won't be able to withdraw ether from this contract, now recall that tx.origin points to the original address that created the transaction so if Eve can trick Alice into calling this function then tx.origin here will be equal to Alice, the owner will be equal alice, and Eve will be able to trick Alice into withdrawing ether from this contract.

```solidity
    /*
    Alice -> Wallet.transfer() (tx.origin = Alice)
    Alice -> Eve's malicious contract -> Wallet.transfer() (tx.origin = Alice)
    */
    function transfer(address payable _to, uint _amount) public {
        require(tx.origin == owner, "Not owner");

        (bool sent, ) = _to.call{value: _amount}("");
        require(sent, "Failed to send Ether");
    }
```

Let me explain what I mean by using a diagram, so if Alice calls the transfer function inside this wallet contract then tx.origin will be equal to Alice, however if Eve can trick Alice into calling her malicious contract which would then call wallet.transfer, then tx.orgin will still be equal to Alice, and by doing this Eve will be able to withdraw ethers from Alice's wallet, so that's the idea of how to do a phishing attack on a contract that uses tx.origin.

```solidity
contract Attack {
    address payable public owner;
    Wallet wallet;

    constructor(Wallet _wallet) public {
        wallet = Wallet(_wallet);
        owner = msg.sender;
    }

    function attack() public {
        wallet.transfer(owner, address(wallet).balance);
    }
}
```

So let's now put this phishing attack into code, our attack contract will store two state variables, the owner of this contract will be the address that will be receiving the ether from this transfer function and the wallet state variable will be our target, we initialize these two state variables using a constructor, now how are we going to exploit the wallet contract? Well, the only thing that we'll have to do is somehow trick Alice into calling this function, and when Alice calls this function, we'll call the transfer function and then withdraw all of the ether from it. We'll be able to withdraw ether from the wallet by calling wallet.transfer and who are we sending the ether to? We're sending it to the owner of this attack contract, how much ether does Eve want to steal from the wallet contract? Well, she'll want to steal all of the ether so here we'll say all of the balance from the wallet contract (**address(wallet).balance**) and that's all we have to do here in order for Eve to steal ether from the wallet contract above.

Let's walk through how this attack works, Eve will somehow trick Alice into calling this function and this we call the transfer function on the wallet contract, the wallet contract checks that the tx.origin is equal to Alice and this check will pass since Alice called this function and all of the ether stored in this contract will be sent to Eve.

Here I've deployed to contract wallet and attack, let's say that Alice is going to deposit 1 ether into her wallet and we can check that this wallet has 1 ether by calling **getBalance**, now if Eve tries to call the **transfer** function and transfer all of the ether to her from Alice's wallet, you can see here that the transaction will fail, but now let's say that Eve tricks Alice into calling the attack function, you can see here that the transaction was successful, so let's check back on the balance of the wallet and the balance is zero, so Eve was able to deceive Alice into sending all of our ether in this wallet to Eve's address.

So how can you prevent this type of phishing attack? Well, we can prevent this attack by simply changing tx.origin to msg.sender, now why would this prevent a phishing attack? Well, this is because msg.sender always points to the contract or the account that called this contract, so in the case where Alice calls Eve's malicious contract which calls wallet.transfer, msg.sender here will be equal to Eve's contract, so the check over here will fail.

All right so that was a video about fishing with tx.origin and the lesson to be learned here is that if you're going to use tx.origin then you should keep in mind that your contract might be vulnerable to phishing attack.